from Base import all
import Base.Time
import Base.Time.Interval

polyglot java import java.time.Instant
polyglot java import java.time.LocalDate

type Date

    ## `Date` represents a date, often viewed as year-month-day. For
       example, the value "2nd October 2007" can be stored in a `Date`.

       This class does not store or represent a time or time-zone. Instead, it is a
       description of the date, as used for birthdays. It cannot represent an
       instant on the time-line without additional information such as an offset or
       time-zone.
    type Date internal_local_date

    ## Get the year field.
    get_year : Integer
    get_year = this . internal_local_date . getYear []

    ## Get the month of year field from 1 to 12.
    get_month : Integer
    get_month = this . internal_local_date . getMonthValue []

    ## Get the day of month field.
    get_day : Integer
    get_day = this . internal_local_date . getDayOfMonth []

    ## Add specified amount of time to this instant.

       > Example
         Add 100 years to a local date.
             Date.new.plus 100.years
    plus : Interval -> Date
    plus amount =
        if amount.is_time then Error.throw (Time.Error "Date does not support time intervals") else Date (this . internal_local_date . plus [amount.interval_period])

    ## Subtract specified amount of time to this instant.

       > Example
         Subtract 1 month from a local date.
             Date.new.minus 1.month
    minus : Period -> Date
    minus amount =
        if amount.is_time then Error.throw (Time.Error "Date does not support time intervals") else Date (this . internal_local_date . minus [amount.interval_period])

## Obtains an instance of `Date` from a text string such as 2007-12-03.

   The string must represent a valid date and is parsed using the ISO-8601
   extended local date format. The format consists of:

   - Four digits or more for the year. Years in the range 0000 to 9999
     will be pre-padded by zero to ensure four digits. Years outside
     that range will have a prefixed positive or negative symbol.

   - A dash
   - Two digits for the month-of-year. This is pre-padded by zero to ensure two
     digits.
   - A dash
   - Two digits for the day-of-month. This is pre-padded by zero to ensure two
     digits.

   > Example
     Parse the date of 23rd December 2020.
         Date.parse "2020-12-23"
parse : Text -> Date
parse fmt = Date (LocalDate.parse [fmt])

## Obtains the current date from the system clock in the default time-zone.
now : Date
now = Date (LocalDate.now [])

## Obtains an instance of `Date` from a year, month and day.

   The day must be valid for the year and month, otherwise an exception will be
   thrown.

   - month - the month-of-year to represent, from 1 (January) to 12 (December)
   - day - the day-of-month to represent, from 1 to 31

   > Example
     Create a new local date at Unix epoch.
         Date.new 1970

   > Example
     Get the local date of 5th August 1986.
         Date.new 1986 8 5
new : Integer -> Integer -> Integer -> Date
new year (month = 1) (day = 1) =
    Date (LocalDate.of [year, month, day])
