from Base import all

polyglot java import java.time.ZoneOffset

## A time-zone offset is the amount of time that a time-zone differs from
   Greenwich/UTC. This is usually a fixed number of hours and minutes.

   Different parts of the world have different time-zone offsets. The rules for
   how offsets vary by place and time of year are captured in the `ZoneId` class.

   For example, Paris is one hour ahead of Greenwich/UTC in winter and two hours
   ahead in summer. The ZoneId instance for Paris will reference two
   `Zone_Offset` instances - a +01:00 instance for winter, and a +02:00 instance
   for summer.
type Zone_Offset internal_zone_offset

## Gets the normalized zone offset ID.
Zone_Offset.get_id : Text
Zone_Offset.get_id = this . internal_zone_offset . getId []

## Gets the total zone offset in seconds.
Zone_Offset.get_total_seconds : Integer
Zone_Offset.get_total_seconds = this . internal_zone_offset . getTotalSeconds []

## Obtains an instance of `Zone_Offset` using the ID.

   This method parses the string ID of a `Zone_Offset` to return an instance. The
   parsing accepts all the formats generated by `Zone_Offset.get_id`, plus some
   additional formats:

   - Z - for UTC
   - +h
   - +hh
   - +hh:mm
   - -hh:mm
   - +hhmm
   - -hhmm
   - +hh:mm:ss
   - -hh:mm:ss
   - +hhmmss
   - -hhmmss

   The maximum supported range is from +18:00 to -18:00 inclusive.

   > Example
     Get time zone -3 hours of Greenwich.
         Zone_Offset.parse "-03:00"
parse : Text -> Zone_Offset
parse text = Zone_Offset (ZoneOffset.of [text])

## Obtains an instance of `Zone_Offset` using an offset in hours, minutes
   and seconds.

   - the time-zone offset in hours, from -18 to +18
   - the time-zone offset in minutes, from 0 to ±59, sign matches hours and
     seconds
   - the time-zone offset in seconds, from 0 to ±59, sign matches hours and
     minutes

   > Example
     Get time zone 1 hour 1 minute and 50 seconds of Greenwich/UTC.
         Zone_Offset.new 1 1 50
new : Number -> Number -> Number -> Zone_Offset
new (hours = 0) (minutes = 0) (seconds = 0) =
    Zone_Offset (ZoneOffset.ofHoursMinutesSeconds [hours, minutes, seconds])

## UTC time zone.
utc : Zone_Offset
utc = here.new
