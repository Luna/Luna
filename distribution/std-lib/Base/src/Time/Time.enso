from Base import all
import Base.Time.Interval
import Base.Time.Zone

polyglot java import java.time.Instant
polyglot java import java.time.ZonedDateTime

type Time

    type Time internal_time

    ## Get the year field.
    get_year : Integer
    get_year = this . internal_time . getYear []

    ## Get the month of year field from 1 to 12.
    get_month : Integer
    get_month = this . internal_time . getMonthValue []

    ## Get the day of month field.
    get_day : Integer
    get_day = this . internal_time . getDayOfMonth []

    ## Get the hour of day field.
    get_hour : Integer
    get_hour = this . internal_time . getHour []

    ## Get the minute of hour field.
    get_minute : Integer
    get_minute = this . internal_time . getMinute []

    ## Get the second of minute field
    get_second : Integer
    get_second = this . internal_time . getSecond []

    ## Gets the nano-of-second field.
    get_nano : Integer
    get_nano = this . internal_time . getNano []

    ## Gets the nano-of-second field.
    get_nanosecond : Integer
    get_nanosecond = this . get_nano

    ## Return the number of seconds from the Unix epoch.
    to_epoch_second : Integer
    to_epoch_second = this . internal_time . toEpochSecond []

    ## Return the number of milliseconds from the Unix epoch.
    to_epoch_milli : Integer
    to_epoch_milli = this . internal_time . toInstant [] . toEpochMilli []

    ## Add specified amount of time to this instant.

       > Example
         Add 1 hour to a zoned date time.
             Time.new.plus 1.hour

       > Example
         Add 15 years and 3 hours to a zoned date time.
             Time.new . plus 15.years . plus 3.hours
    plus : Interval -> Time
    plus amount = Time (this . internal_time . plus [amount.interval_period] . plus [amount.interval_duration])

    ## Subtract specified amount of time to this instant.

       > Example
         Subtract 10 days from a zoned date time.
             Time.new.minus 10.days

       > Example
         Subtract 1 year and 9 months from a zoned date time.
             Time.new.minus 1.year . minus 9.months
    minus : Interval -> Time
    minus amount = Time (this . internal_time . minus [amount.interval_period] . minus [amount.interval_duration])

## Obtains an instance of `Time` from a text string such as
   2007-12-03T10:15:30Z.

   The string must represent a valid date-time and is parsed using the ISO-8601
   extended offset date-time format to add the time-zone. The section in square
   brackets is not part of the ISO-8601 standard. The format consists of:

   - The ISO offset date time.
   - If the zone ID is not available or is a zone offset then the format is
     complete.
   - An open square bracket '['.
   - The zone ID. This is not part of the ISO-8601 standard. Parsing is case
     sensitive.
   - A close square bracket ']'.

   > Example
     Parse UTC time.
         Time.parse "2020-10-01T04:11:12Z"

   > Example
     Parse UTC-04:00 time.
         Time.parse "2020-10-01T04:11:12-04:00"

   > Example
     Parse UTC-04:00 time specifying New York time zone.
         Time.parse "2020-10-01T04:11:12-04:00[America/New_York]"

   > Example
     Parse UTC-04:00 time with nanoseconds.
         Time.parse "2020-10-01T04:11:12.177528-04:00"
parse : Text -> Time
parse text = Time (ZonedDateTime.parse [text])

## Obtains the current date-time from the system clock in the default time-zone.
now : Time
now = Time (Instant.now [] . atZone [Zone.utc . internal_zone])

## Obtains an instance of `Time` from a year, month, day, hour, minute,
   second, nanosecond and time-zone.

   The day must be valid for the year and month, otherwise an exception will be
   thrown.

   - month - the month-of-year to represent, from 1 (January) to 12 (December)
   - day - the day-of-month to represent, from 1 to 31
   - hour - the hour-of-day to represent, from 0 to 23
   - minute - the minute-of-hour to represent, from 0 to 59
   - second - the second-of-minute to represent, from 0 to 59
   - nanosecond - the nano-of-second to represent, from 0 to 999,999,999
   - zone - the time-zone

   > Example
     Create a new zoned date time at Unix epoch.
         Time.new 1970

   > Example
     Get the 5 August 1986 at midnight UTC.
         Time.new 1986 8 5
new : Integer -> Integer -> Integer -> Integer -> Integer -> Integer -> Integer -> Zone -> Time
new year (month = 1) (day = 1) (hour = 0) (minute = 0) (second = 0) (nanosecond = 0) (offset = Zone.utc) =
    Time (ZonedDateTime.of [year, month, day, hour, minute, second, nanosecond, offset.internal_zone])
