from Base import all
import Base.Time
import Base.Time.Interval

polyglot java import java.time.Instant
polyglot java import java.time.LocalDate

type Local_Date

    ## `Local_Date` is an immutable date-time object that represents a date, often
       viewed as year-month-day. Other date fields, such as day-of-year, day-of-week
       and week-of-year, can also be accessed. For example, the value
       "2nd October 2007" can be stored in a `Local_Date`.

       This class does not store or represent a time or time-zone. Instead, it is a
       description of the date, as used for birthdays. It cannot represent an
       instant on the time-line without additional information such as an offset or
       time-zone.
    type Local_Date internal_local_date

    ## Get the year field.
    get_year : Integer
    get_year = this . internal_local_date . getYear []

    ## Get the month of year field from 1 to 12.
    get_month : Integer
    get_month = this . internal_local_date . getMonthValue []

    ## Get the day of month field.
    get_day : Integer
    get_day = this . internal_local_date . getDayOfMonth []

    ## Add specified amount of time to this instant.

       > Example
         Add 100 years to a local date.
             Local_Date.new.plus 100.years
    plus : Interval -> Local_Date
    plus amount =
        if amount.is_time then Error.throw (Time.Error "Date does not support time intervals") else Local_Date (this . internal_local_date . plus [amount.interval_period])

    ## Subtract specified amount of time to this instant.

       > Example
         Subtract 1 month from a local date.
             Local_Date.new.minus 1.month
    minus : Period -> Local_Date
    minus amount =
        if amount.is_time then Error.throw (Time.Error "Date does not support time intervals") else Local_Date (this . internal_local_date . minus [amount.interval_period])

## Obtains an instance of `Local_Date` from a text string such as 2007-12-03.

   The string must represent a valid date and is parsed using the ISO-8601
   extended local date format. The format consists of:

   - Four digits or more for the year. Years in the range 0000 to 9999
     will be pre-padded by zero to ensure four digits. Years outside
     that range will have a prefixed positive or negative symbol.

   - A dash
   - Two digits for the month-of-year. This is pre-padded by zero to ensure two
     digits.
   - A dash
   - Two digits for the day-of-month. This is pre-padded by zero to ensure two
     digits.

   > Example
     Parse the date of 23rd December 2020.
         Local_Date.parse "2020-12-23"
parse : Text -> Local_Date
parse fmt = Local_Date (LocalDate.parse [fmt])

## Obtains the current date from the system clock in the default time-zone.
now : Local_Date
now = Local_Date (LocalDate.now [])

## Obtains an instance of `Local_Date` from a year, month and day.

   The day must be valid for the year and month, otherwise an exception will be
   thrown.

   - month - the month-of-year to represent, from 1 (January) to 12 (December)
   - day - the day-of-month to represent, from 1 to 31

   > Example
     Create a new local date at Unix epoch.
         Local_Date.new

   > Example
     Get the local date of 5th August 1986.
         Local_Date.new 1986 8 5
new : Integer -> Integer -> Integer -> Local_Date
new (year = 1970) (month = 1) (day = 1) =
    Local_Date (LocalDate.of [year, month, day])
