from Base import all

polyglot java import java.time.ZoneId
polyglot java import java.time.ZoneOffset

## A `Zone_Id` is used to identify the rules used to convert between an
   `Instant` and a `LocalDateTime`. There are two distinct types of ID:

   - Fixed offsets - a fully resolved offset from UTC/Greenwich, that uses the
     same offset for all local date-times
   - Geographical regions - an area where a specific set of rules for finding
     the offset from UTC/Greenwich apply
type Zone_Id internal_zone_id

## Gets the unique time-zone ID.
Zone_Id.get_id : Text
Zone_Id.get_id = this . internal_zone_id . getId []

## This method parses the ID producing a `Zone_Id`.

   Parsing matches the zone ID step by step as follows.

   - If the zone ID equals 'Z', the result is `Zone_Offset.utc`.

   - If the zone ID consists of a single letter, the zone ID is invalid and
     `DateTimeException` is thrown.

   - If the zone ID starts with '+' or '-', the ID is parsed as a `Zone_Offset`
     using `Zone_Offset.of Text`.

   - If the zone ID equals 'GMT', 'UTC' or 'UT' then the result is a `Zone_Id`
     with the same ID and rules equivalent to `Zone_Offset.utc`.

   - If the zone ID starts with 'UTC+', 'UTC-', 'GMT+', 'GMT-', 'UT+' or 'UT-'
     then the ID is a prefixed offset-based ID. The ID is split in two, with a
     two or three letter prefix and a suffix starting with the sign. The suffix
     is parsed as a `Zone_Offset`. The result will be a `Zone_Id` with the
     specified UTC/GMT/UT prefix and the normalized offset. The rules of the
     returned `Zone_Id` will be equivalent to the parsed `Zone_Offset`.

   - All other IDs are parsed as region-based zone IDs. Region IDs must match
     the regular expression [A-Za-z][A-Za-z0-9~/._+-]+ otherwise a
     `DateTimeException` is thrown. If the zone ID is not in the configured set
     of IDs, `ZoneRulesException` is thrown. The detailed format of the region
     ID depends on the group supplying the data. The default set of data is
     supplied by the IANA Time Zone Database (TZDB). This has region IDs of the
     form '{area}/{city}', such as 'Europe/Paris' or 'America/New_York'. This is
     compatible with most IDs from `TimeZone`.

   > Example
     Get Central European Time.
         Zone_Id.parse "CET"

   > Example
     Get Moscow time.
         Zone_Id.parse "Europe/Moscow"

   > Example
     Get time zone -06:00.
         Zone_Id.parse "-06:00"
parse : Text -> Zone_Id
parse text = Zone_Id (ZoneId.of [text])

## Obtains an instance of `ZoneId` wrapping an offset.

   If the prefix is "GMT", "UTC", or "UT" a `Zone_Id` with the prefix and the
   non-zero offset is returned. If the prefix is empty "" the `Zone_Offset` is
   returned.
of_offset : Text -> Zone_Offset -> Zone_Id
of_offset prefix offset = Zone_Id (ZoneId.ofOffset [prefix, offset.internal_zone_offset])

## The system default time-zone.
system : Zone_Id
system = Zone_Id (ZoneId.systemDefault [])

## UTC time zone.
utc : Zone_Id
utc = here.parse "UTC"
