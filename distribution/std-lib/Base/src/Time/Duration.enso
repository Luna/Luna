from Base import all
import Base.Time.Time

polyglot java import java.time.Duration as Java_Duration
polyglot java import java.time.Period as Java_Period

type Duration

    ## An amount of time in terms of years, months, days, seconds and
       nanoseconds.
    type Duration interval_period interval_duration

    ## Add specified amount of time to this duration.

       > Example
         Add 1 minute to a duration of 12 hours
             12.hours . plus 1.minute

       > Example
         Add 1 hour to a duration of month.
             1.month . plus 1.hour
    plus : Duration -> Duration
    plus other = Duration (this.interval_period . plus [other.interval_period]) (this.interval_duration . plus [other.interval_duration])

    ## Subtract specified amount of time from this duration.
       > Example
         Subtract 1 nanosecond from a duration of 120 nanoseconds.
             120.nano . minus 1.nano

       > Example
         Substract 3 hours from a duration of 12 years.
             12.years . minus 3.hours
    minus : Duration -> Duration
    minus other = Duration (this.interval_period . minus [other.interval_period]) (this.interval_duration . minus [other.interval_duration])

    ## Get the amount of nanoseconds of this duration.
    get_nanos : Integer
    get_nanos = this.interval_duration . toNanosPart []

    ## Get the amount of milliseconds of this duration.
    get_millis : Integer
    get_millis = this.interval_duration . toMillisPart []

    ## Get the amount of minutes of this duration.
    get_seconds : Integer
    get_seconds = this.interval_duration . toSecondsPart []

    ## Get the amount of minutes of this duration.
    get_minutes : Integer
    get_minutes = this.interval_duration . toMinutesPart []

    ## Get the amount of hours of this duration.
    get_hours : Integer
    get_hours = this.interval_duration . toHours []

    ## Get the amount of days of this duration.
    get_days : Integer
    get_days = this.interval_period . getDays []

    ## Get the amount of months of this duration.
    get_months : Integer
    get_months = this.interval_period . getMonths []

    ## Get the amount of days of this duration.
    get_years : Integer
    get_years = this.interval_period . getYears []

    ## Convert this duration to a Vector.

       > Example
         Convert duration of a year and a hour to a vector returning `[1, 0, 0, 1, 0, 0, 0]`.
             1.year . plus 1.hour . to_vector

       > Example
         Convert duration of 800 nanoseconds to a vector returning `[0, 0, 0, 0, 0, 0, 800]`
             800.nanoseconds . to_vector
    to_vector : Vector
    to_vector = [this.get_years, this.get_months, this.get_days, this.get_hours, this.get_minutes, this.get_seconds, this.get_nanos]

    ## Check if this duration is date-based.
    is_date : Boolean
    is_date = (not this.get_years==0) || (not this.get_months==0) || (not this.get_days==0)

    ## Check if this duration is time-based.
    is_time : Boolean
    is_time = (not this.get_hours==0) || (not this.get_minutes==0) || (not this.get_seconds==0) || (not this.get_nanos==0)

    ## Check if this duration represent an empty time-span.
    is_empty : Boolean
    is_empty = (not this.is_date) && (not this.is_time)

## Duration in nanoseconds.
Integer.nano : Duration
Integer.nano = Duration (Java_Period.ofDays [0]) (Java_Duration.ofNanos [this])

## Duration in nanoseconds.
Integer.nanos : Duration
Integer.nanos = this.nano

## Duration in nanoseconds.
Integer.nanosecond : Duration
Integer.nanosecond = this.nano

## Duration in nanoseconds.
Integer.nanoseconds : Duration
Integer.nanoseconds = this.nano

## Duration in milliseconds.
Integer.milli : Duration
Integer.milli = Duration (Java_Period.ofDays [0]) (Java_Duration.ofMillis [this])

## Duration in milliseconds.
Integer.millis : Duration
Integer.millis = this.milli

## Duration in milliseconds.
Integer.millisecond : Duration
Integer.millisecond = this.milli

## Duration in milliseconds.
Integer.milliseconds : Duration
Integer.milliseconds = thils.milli

## Duration in seconds.
Integer.second : Duration
Integer.second = Duration (Java_Period.ofDays [0]) (Java_Duration.ofSeconds [this])

## Duration in seconds.
Integer.seconds : Duration
Integer.seconds = this.second

## Duration in minutes.
Integer.minute : Duration
Integer.minute = Duration (Java_Period.ofDays [0]) (Java_Duration.ofMinutes [this])

## Duration in minutes.
Integer.minutes : Duration
Integer.minutes = this.minute

## Duration in hours.
Integer.hour : Duration
Integer.hour = Duration (Java_Period.ofDays [0]) (Java_Duration.ofHours [this])

## Duration in hours.
Integer.hours : Duration
Integer.hours = this.hour

## Duration in days.
Integer.day : Duration
Integer.day = Duration (Java_Period.ofDays [this]) (Java_Duration.ofSeconds [0])

## Duration in days.
Integer.days : Duration
Integer.days = this.day

## Duration in months.
Integer.month : Duration
Integer.month = Duration (Java_Period.ofMonths [this]) (Java_Duration.ofSeconds [0])

## Duration in months.
Integer.months : Duration
Integer.months = this.month

## Duration in years.
Integer.year : Duration
Integer.year = Duration (Java_Period.ofYears [this]) (Java_Duration.ofSeconds [0])

## Duration in years.
Integer.years : Duration
Integer.years = this.year

## Create an interval representing the duration between two points in time.

   > Example
     An hour interval between two points in time.
         Duration.between Time.now Time.now
between : Time -> Time -> Duration
between start_inclusive end_exclusive =
   Duration (Java_Period.ofDays [0]) (Java_Duration.between [start_inclusive.internal_zoned_date_time, end_exclusive.internal_zoned_date_time])
