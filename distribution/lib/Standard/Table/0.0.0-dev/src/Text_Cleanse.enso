from Standard.Base import all
import project.Column.Column
import project.Internal.Widget_Helpers

## Defines a Text_Cleanse operation
type Text_Cleanse
   Duplicate_Whitespace
   Leading_Whitespace
   Trailing_Whitespace
   All_Whitespace
   Leading_Numbers
   Trailing_Numbers
   Non_ASCII
   Tabs
   Letters
   Numbers
   Punctuation
   Symbols

   ## Apply the cleanse operation to the text.
   apply self input:Cleansable_Text -> Any =
      case self of
         Text_Cleanse.Leading_Whitespace -> input.replace "^\s+" ""
         Text_Cleanse.Trailing_Whitespace -> input.replace "\s+$" ""
         Text_Cleanse.Duplicate_Whitespace -> input.replace "(\s)+" "$1"
         Text_Cleanse.All_Whitespace -> input.replace "\s+" ""
         Text_Cleanse.Leading_Numbers -> input.replace "^\d+" ""
         Text_Cleanse.Trailing_Numbers -> input.replace "\d+$" ""
         Text_Cleanse.Non_ASCII -> input.replace "[^\x00-\x7F]" ""
         Text_Cleanse.Tabs -> input.replace "\t" ""
         Text_Cleanse.Letters -> input.replace "[a-zA-Z]" ""
         Text_Cleanse.Numbers -> input.replace "\d" ""
         Text_Cleanse.Punctuation -> input.replace '[,.!?():;\'\"]' ""
         Text_Cleanse.Symbols -> input.replace "[^a-zA-Z\d\s]" ""

## PRIVATE 
   Defines the interface for cleansable object.
type Cleansable_Text
   Value replace

## PRIVATE
Cleansable_Text.from (that:Text) = Cleansable_Text.Value (pattern->replace_with-> that.replace (regex pattern) replace_with)

## PRIVATE
Cleansable_Text.from (that:Column) = Cleansable_Text.Value (pattern->replace_with-> (that.text_replace (regex pattern) replace_with).rename that.name)


## GROUP Text
   ICON text
   Removes the specified cleansings from the text.

   Arguments:
   - remove: A vector of the text cleanings to remove from the text. The text cleansing are 
      applied in the order they are provided. The same text cleansing can be used multiple
      times. The text cleansings are:
      - ..Leading_Whitespace: Removes all whitspace from the start of the string.
      - ..Trailing_Whitespace: Removes all whitspace from the end of the string.
      - ..Duplicate_Whitespace: Removes all duplicate whitspace from the string replacing it with the first whitespace character of the duplicated block.
      - ..All_Whitespace: Removes all whitspace from the string.
      - ..Leading_Numbers: Removes all numbers from the start of the string.
      - ..Trailing_Numbers: Removes all numbers from the end of the string.
      - ..Non_ASCII: Removes all non-ascii characters from the string.
      - ..Tabs: Removes all tab characters from the string.
      - ..Letters: Removes all letters from the string.
      - ..Numbers: Removes all numbers characters from the string.
      - ..Punctuation: Removes all characters in the set ,.!?():;'" from the string.
      - ..Symbols: Removes anything that isn't letters, numbers or whitespace from the string.

   > Example
   Remove leading and trailing spaces from text.

         text.cleanse [..Leading_Whitespace, ..Trailing_Whitespace]
@remove Widget_Helpers.make_data_cleanse_vector_selector
Text.cleanse : Vector Text_Cleanse -> Text
Text.cleanse self remove =
   remove.fold self (current-> tc-> tc.apply current)
