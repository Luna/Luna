from Standard.Base import all

import Standard.Table.Data.Table

from Standard.Table.Excel.Section import Excel_Section, Worksheet, Sheet_Names, Range_Names
from Standard.Table.Excel.Reader import read_excel
from Standard.Table.Excel.Writer import write_excel

## PRIVATE
   Resolve the xls_format setting to a boolean.
is_xls_format : (Boolean|Infer) -> File -> Boolean
is_xls_format xls_format file =
    if xls_format != Infer then xls_format else
        extension = file.extension
        (extension.equals_ignore_case ".xls") || (extension.equals_ignore_case ".xlt")

## Read the file to a `Table` from an Excel file
type Excel_Format
    ## Read Excels files into a Table or Vector.

       Arguments:
       - section: The `Excel_Section` to read from the workbook.
         This can be one of:
         - `Sheet_Names` - outputs a `Vector` of sheet names.
         - `Range_Names` - outputs a `Vector` of range names.
         - `Worksheet` - outputs a `Table` containing the specified sheet.
         - `Cell_Range` - outputs a `Table` containing the specified range.
       - headers: If set to `True`, the first row is used as column names. If
         set to `False`, the column names are Excel column names. If set to
         `Infer`, the process tries to infer if headers are present on the first
          row. If the column names are not unique, numeric suffixes will be
          appended to disambiguate them.
       - xls_format:
         If set to `True`, the file is read as an Excel 95-2003 format.
         If set to `False`, the file is read as an Excel 2007+ format.
         `Infer` will attempt to deduce this from the extension of the filename.
    Excel (section:Excel_Section=Worksheet) (headers:(Boolean|Infer)=Infer) (xls_format:(Boolean|Infer)=Infer)

    ## If the File_Format supports reading from the file, return a configured instance.
    can_read : File -> Any
    can_read self file =
        case file.extension of
            ".xlsx" -> Excel xls_format=False
            ".xlsm" -> Excel xls_format=False
            ".xls" -> Excel xls_format=True
            ".xlt" -> Excel xls_format=True
            _ -> Nothing

    ## Implements the `File.read` for this `File_Format`
    read : File -> Problem_Behavior -> Any
    read self file on_problems =
        format = is_xls_format self.xls_format file
        read_excel file self.section self.headers on_problems format

    ## Implements the `Table.write` for this `File_Format`.
    write_table : File -> Table -> Existing_File_Behavior -> Match_Columns -> Problem_Behavior -> Nothing
    write_table self file table on_existing_file match_columns on_problems =
        format = is_xls_format self.xls_format file

        case self.section of
            Sheet_Names -> Error.throw (Illegal_Argument_Error "Sheet_Names cannot be used for `write`.")
            Range_Names -> Error.throw (Illegal_Argument_Error "Range_Names cannot be used for `write`.")
            _ -> write_excel file table on_existing_file self.section self.headers match_columns on_problems format
