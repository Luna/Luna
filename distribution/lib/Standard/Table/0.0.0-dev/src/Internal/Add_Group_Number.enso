from Standard.Base import all
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument

import project.Column.Column
import project.Grouping_Method.Grouping_Method
import project.Internal.Java_Problems
import project.Internal.Problem_Builder.Problem_Builder
import project.Internal.Table_Helpers
import project.Set_Mode.Set_Mode
import project.Table.Table
from project.Internal.Add_Row_Number import rename_columns_if_needed

polyglot java import org.enso.table.operations.AddGroupNumber

add_group_number (table:Table) (grouping_method:Grouping_Method=..Unique) (name:Text="Group") (from:Integer=0) (step:Integer=1) (group_by:(Vector | Text | Integer | Regex)=[]) (order_by:(Vector | Text)=[]) (on_problems:Problem_Behavior=..Report_Warning) -> Table =
    problem_builder = Problem_Builder.new error_on_missing_columns=True
    grouping_columns = table.columns_helper.select_columns_helper group_by Case_Sensitivity.Default True problem_builder
    ordering = Table_Helpers.resolve_order_by table.columns order_by problem_builder

    problem_builder.attach_problems_before on_problems <|
        new_column = create_column table grouping_method name from step grouping_columns ordering on_problems
        renamed_table = rename_columns_if_needed table name on_problems Table.new
        renamed_table.set new_column name set_mode=Set_Mode.Add

## PRIVATE
create_column table grouping_method name from step grouping_columns ordering on_problems =
    ordering_columns = ordering.map c->c.column.java_column
    directions = ordering.map c->c.associated_selector.direction.to_sign
    grouping_java_columns = grouping_columns.map c->c.java_column
    new_storage = Java_Problems.with_problem_aggregator on_problems java_problem_aggregator->
        create_column_with_method table.row_count grouping_method from step grouping_java_columns ordering_columns directions java_problem_aggregator
    Column.from_storage name new_storage

## PRIVATE
create_column_with_method row_count grouping_method from step grouping_java_columns ordering_columns directions java_problem_aggregator =
    case grouping_method of
        Grouping_Method.Unique ->
            if grouping_java_columns.is_empty || ordering_columns.is_empty.not then Error.throw (Illegal_Argument.Error "add_group_number with ..Unique requires a non-empty 'group_by' and cannot take an 'order_by' parameter") else
                AddGroupNumber.numberGroupsUnique row_count from step grouping_java_columns java_problem_aggregator
        Grouping_Method.Equal_Count bucket_count ->
            if grouping_java_columns.is_empty.not then Error.throw (Illegal_Argument.Error "add_group_number with ..Equal_Count cannot take a 'group_by' parameter") else
                AddGroupNumber.numberGroupsEqualCount row_count bucket_count from step ordering_columns directions java_problem_aggregator
