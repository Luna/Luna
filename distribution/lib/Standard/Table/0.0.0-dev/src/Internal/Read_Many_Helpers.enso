private

from Standard.Base import all
import Standard.Base.Data.Read.Many_Files_List.Many_Files_List
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument

import project.Column.Column
import project.Errors.Invalid_Value_Type
import project.Extensions.Table_Conversions
import project.Return_As_Table.Return_As_Table
import project.Table.Table
import project.Value_Type.Value_Type
from project.Table import from_java_table

polyglot java import org.enso.table.data.mask.OrderMask

find_files_list_in_table (that : Table) -> Many_Files_List =
    found_column = if that.column_count == 1 then that.at 0 else
        path_columns = that.select_columns "path" case_sensitivity=..Insensitive on_problems=..Report_Error
        not_found = path_columns.is_error || (path_columns.column_count == 0)
        if not_found then Error.throw (Illegal_Argument.Error "To use a Table as file list, it must be a single column or contain a `path` column (case insensitive).") else
            if path_columns.column_count > 1 then Error.throw (Illegal_Argument.Error "Multiple 'paths' column candidates found: "+path_columns.column_names.to_display_text+".") else
                path_columns.at 0
    ensure_column_type_valid_to_be_files_list found_column <|
        Many_Files_List.Value that found_column.to_vector Return_As_Table.Merged_Table

ensure_column_type_valid_to_be_files_list (column : Column) ~action =
    is_expected_type = case column.value_type of
        # Columns containing File objects will be Mixed
        Value_Type.Mixed -> True
        # Columns containing paths as Text will be Char
        Value_Type.Char _ _ -> True
        _ -> False
    if is_expected_type then action else
        Error.throw (Invalid_Value_Type.Column "Text or Mixed" column.value_type column.name)

make_return (return_shape : Return_As_Table) (input : Many_Files_List) (objects : Vector Any) -> Table =
    base_table = _input_as_table input
    case return_shape of
        Return_As_Table.Table_Of_Objects ->
            _add_objects_column base_table objects
        Return_As_Table.Merged_Table columns_to_keep match_columns ->
            tables = objects.map _interpret_object_as_table
            _merge_input_and_tables base_table tables columns_to_keep match_columns

_input_as_table (input : Many_Files_List) -> Table =
    case input.original_value of
        table : Table -> table
        column : Column -> column.to_table
        ## Fallback - any unknown input shape is treated the same as
           Vector input - we just extract the list of files from it
        _ ->
            Table.new [["Path", input.paths_to_load]]

## Expands each table from `tables_for_rows` and computes their union, also
   duplicating the corresponding rows from `input_table`.
_merge_input_and_tables (input_table : Table) (tables_for_rows : Vector Table) columns_to_keep match_columns -> Table =
    no_rows = tables_for_rows.is_empty || tables_for_rows.all .is_nothing
    if no_rows then input_table else
        adapted_tables = tables_for_rows.map table->
            if (table.is_a Table . not) || (table.row_count > 0) then table else
                _make_table_with_one_null_row table
        counts = adapted_tables.map table-> case table of
            Nothing -> 1
            _ : Table -> table.row_count
        unified_tables = Table.from_union adapted_tables columns_to_keep match_columns
        multiplicated_inputs = _duplicate_rows input_table counts
        Runtime.assert (unified_tables.row_count == multiplicated_inputs.row_count)
        multiplicated_inputs.zip unified_tables right_prefix=""

_make_table_with_one_null_row (empty_table : Table) -> Table =
    Runtime.assert (empty_table.row_count == 0) "The table must be empty."
    Table.new (empty_table.columns.map c-> Column.from_vector c.name [Nothing] c.value_type)

_add_objects_column (base_table : Table) (objects : Vector Any) -> Table  =
    unique_naming = base_table.column_naming_helper.create_unique_name_strategy
    unique_naming.mark_used base_table.column_names
    objects_column_name = unique_naming.make_unique "Value"
    base_table.set (Column.from_vector objects_column_name objects) as=objects_column_name set_mode=..Add

_interpret_object_as_table (object : Any) -> Table | Nothing =
    default =
        Table.from_objects object treat_dictionary_as_sequence=False
    case object of
        table : Table -> table
        vector : Vector ->
            if vector.is_empty then Nothing else default
        _ -> default

## Takes a table and a list of counts and returns a new table with the rows
   duplicated according to the counts.
   The vector should have the same number of elements as the number of rows in the table.
_duplicate_rows (table : Table) (counts : Vector Integer) -> Table =
    Runtime.assert (table.row_count == counts.size) "The number of counts must match the number of rows in the table."
    mask_array = counts
        . map_with_index ix-> count-> Vector.fill count ix
        . flatten
    mask = OrderMask.fromArray mask_array
    from_java_table (table.java_table.applyMask mask)
