private

from Standard.Base import all
import Standard.Base.Data.Read.Many_Files_List.Many_Files_List
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument

import project.Column.Column
import project.Errors.Invalid_Value_Type
import project.Return_As_Table.Return_As_Table
import project.Table.Table
import project.Value_Type.Value_Type

find_files_list_in_table (that : Table) -> Many_Files_List =
    found_column = if that.column_count == 1 then that.at 0 else
        path_columns = that.select_columns "path" case_sensitivity=..Insensitive on_problems=..Report_Error
        not_found = path_columns.is_error || (path_columns.column_count == 0)
        if not_found then Error.throw (Illegal_Argument.Error "To use a Table as file list, it must be a single column or contain a `path` column (case insensitive).") else
            if path_columns.column_count > 1 then Error.throw (Illegal_Argument.Error "Multiple 'paths' column candidates found: "+path_columns.column_names.to_display_text+".") else
                path_columns.at 0
    ensure_column_type_valid_to_be_files_list found_column <|
        Many_Files_List.Value that found_column.to_vector Return_As_Table.Merged_Table

ensure_column_type_valid_to_be_files_list (column : Column) ~action =
    is_expected_type = case column.value_type of
        # Columns containing File objects will be Mixed
        Value_Type.Mixed -> True
        # Columns containing paths as Text will be Char
        Value_Type.Char _ _ -> True
        _ -> False
    if is_expected_type then action else
        Error.throw (Invalid_Value_Type.Column "Text or Mixed" column.value_type column.name)

make_return (return_shape : Return_As_Table) (input : Many_Files_List) (objects : Vector Any) -> Table =
    r = _merge_input_and_objects input objects
    base_table = r.first
    objects_column_name = r.second
    case return_shape of
        Return_As_Table.Table_Of_Objects ->
            base_table
        Return_As_Table.Merged_Table ->
            _expand_objects base_table objects_column_name

_merge_input_and_objects (input : Many_Files_List) (objects : Vector Any) -> Pair Table Text =
    base_table = case input.original_value of
        table : Table -> table
        column : Column -> column.to_table
        ## Fallback - any unknown input shape is treated the same as
           Vector input - we just extract the list of files from it
        _ ->
            Table.new [["Path", input.paths_to_load]]

    unique_naming = base_table.column_naming_helper.create_unique_name_strategy
    unique_naming.mark_used base_table.column_names
    objects_column_name = unique_naming.make_unique "Value"
    new_table = base_table
        . set (Column.from_vector objects_column_name objects) as=objects_column_name set_mode=..Add
    Pair.new new_table objects_column_name

_expand_objects (table : Table) (objects_column_name:Text) -> Table =
    table
        . expand_to_rows objects_column_name at_least_one_row=True sequences_only=True
        . expand_column objects_column_name prefix=..None
