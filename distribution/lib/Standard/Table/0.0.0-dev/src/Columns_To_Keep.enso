from Standard.Base import Vector, Text
from Standard.Base.Metadata import make_single_choice, Widget

## Specifies which columns to keep in a union operation.
type Columns_To_Keep
    ## All columns are kept.

       If a column is present only in some of the tables, it is padded with
       `Nothing` for tables where it is missing.
    In_Any

    ## Only columns that are present in all tables are kept.

       If there are columns that are only present in some of the tables,
       a problem is reported.
    In_All

    ## Specific list of column names to keep.

       If a table does not have a column that is specified in the list, it is
       padded with `Nothing` and a problem is reported.
    In_List (column_names : Vector Text)

    ## PRIVATE
       Same as `In_Any`, but it will warn about columns that are not present in
       all tables.
    In_Any_Warn_On_Missing

    ## PRIVATE
       The default widget for `Columns_To_Keep`.
       It does not display the internal `In_Any_Warn_On_Missing` variant, since
       that variant is only meant to be used as the default value.
    default_widget -> Widget =
        make_single_choice <|
            ["In_Any", "In_All", "In_List"].map c-> [c, ".."+c]
