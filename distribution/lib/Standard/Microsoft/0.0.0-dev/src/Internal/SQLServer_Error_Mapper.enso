from Standard.Base import all

from Standard.Database.Errors import Invariant_Violation, SQL_Error

## PRIVATE
type SQLServer_Error_Mapper

    ## PRIVATE
    is_primary_key_violation : SQL_Error -> Boolean
    is_primary_key_violation error =
        ## TODO the SQL error actually contains the duplicated primary key value!
           We could use that to avoid a separate `Non_Unique_Key_Recipe` query.
        error.java_exception.getMessage.contains "Violation of PRIMARY KEY constraint"

    ## PRIVATE
    is_table_already_exists_error : SQL_Error -> Boolean
    is_table_already_exists_error error =
        error.java_exception.getMessage.match ".*There is already an object named .* in the database."

    ## PRIVATE
    transform_custom_errors : SQL_Error -> Any
    transform_custom_errors error =
        message = error.java_exception.getMessage
        if message.contains "ENSO INVARIANT VIOLATED" . not then error else
            payloads = message.tokenize "\[ENSO INVARIANT VIOLATED: (.*)\]"
            if payloads.length != 1 then error else
                Invariant_Violation.Error payloads.first error
