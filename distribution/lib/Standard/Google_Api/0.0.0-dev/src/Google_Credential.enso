from Standard.Base import all
from Standard.Base.Metadata import make_single_choice, Widget

polyglot java import com.google.api.gax.core.CredentialsProvider
polyglot java import com.google.api.gax.core.FixedCredentialsProvider
polyglot java import com.google.auth.oauth2.GoogleCredentials

polyglot java import org.enso.aws.ProfileReader

type Google_Credential

    ## ICON key
       Access using a json credentials file downloaded from your Google account.

       Arguments:
       - file: Path to the json credentials file.
    From_File file:File

    ## ICON key
       Access using the defaults provided by the environment variables.
    Default

    ## PRIVATE
    as_java : CredentialsProvider
    as_java self = case self of
        Google_Credential.From_File file -> 
            google_credentials = file.with_input_stream [File_Access.Read] stream->
                stream.with_java_stream is->
                    GoogleCredentials.fromStream is
            FixedCredentialsProvider.create google_credentials
        Google_Credential.Default -> Nothing
