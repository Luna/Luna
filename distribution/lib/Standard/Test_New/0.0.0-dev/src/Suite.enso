
from Standard.Base import all
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
import Standard.Base.Runtime.State

import project.Group.Group
import project.Group.Group_Builder
import project.Helpers
import project.Suite_Config.Suite_Config
import project.Test_Reporter

polyglot java import java.lang.StringBuilder

type Suite_Builder
    ## PRIVATE

       Arguments:
       - builder: Vector builder that holds all the groups.
    Impl builder

    ## Add a group to the suite builder.

       Arguments:
       - name: Name of the group. When collecting multiple groups into a single suite, all the
         group names must be unique. Otherwise, a panic is thrown.
       - fn: A callback function that takes `Group_Builder` as an argument. You can call the
         `specify` method on group builder. See its docs.
       - pending: Contains a reason for why the test group should be ignored. If Nothing, the test
         is not ignored.
    group : Text -> (Group_Builder -> Any) -> Nothing
    group self (name:Text) (fn : (Group_Builder -> Any)) (pending : (Text | Nothing) = Nothing) =
        group_builder = Group_Builder.Impl
        fn group_builder
        group = Group.Impl name group_builder.teardown_ref.get group_builder.builder.to_vector pending
        self.builder.append group


## A testing suite that holds all the groups with test specs.

   Users are expected to build the suite via `Suite_Builder`. That is, via `Test.build` method.
type Suite
    ## PRIVATE
    Impl (groups : Vector Group)

    run_all self =
        self.run_with_filter Nothing Nothing

    ## Run only tests that match the provided filters. The filters are converted to
       a regular expression. If a filter is Nothing, it matches all the names.

       Arguments:
       - group_filter: Filter for group names.
       - spec_filter: Filter for spec names.
    run_with_filter : (Regex | Text | Nothing) -> (Regex | Text | Nothing) -> Nothing
    run_with_filter self group_filter=Nothing spec_filter=Nothing =
        config = Suite_Config.from_environment
        convert_filter filter =
            case filter of
                r : Regex -> r
                t : Text -> Regex.compile t
                Nothing -> Regex.compile ".*"
                _ -> Error.throw (Illegal_Argument.Error "Wrong specification of a filter: "+filter.to_text)
        group_filter_conv = convert_filter group_filter
        spec_filter_conv = convert_filter spec_filter
        filtered_groups = self.groups.filter group->
            group_name_matches = (group_filter_conv.match group.name) != Nothing
            case group_name_matches of
                False -> False
                True -> group.pending == Nothing
        junit_sb_builder = if config.should_output_junit then StringBuilder.new else Nothing
        Test_Reporter.wrap_junit_testsuites config junit_sb_builder <|
            filtered_groups.each group->
                results = Helpers.run_group_with_filter group spec_filter_conv
                Test_Reporter.print_report results config junit_sb_builder

    group_names self =
        self.groups.map (_.name)

    print_all self =
        IO.println "Test Suite:"
        self.groups.each group->
            IO.println <| "  Group '" + group.name + "':"
            group.specs.each spec->
                IO.println <| "    " + spec.name
