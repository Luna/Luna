from Standard.Base import all

import project.AWS_Credential.AWS_Credential
import project.Errors.S3_Error
import project.Internal.Auth

polyglot java import software.amazon.awssdk.services.s3.S3Client
polyglot java import software.amazon.awssdk.services.s3.model.S3Exception

type S3
    ## Gets the list of the S3 buckets.
    @credentials AWS_Credential.default_widget
    list_buckets : AWS_Credential | Nothing -> Vector Text ! S3_Error
    list_buckets credentials:(AWS_Credential | Nothing)=Nothing = catch_s3 <|
        client = make_client credentials
        buckets = client.listBuckets.buckets
        buckets.map b->b.name

## PRIVATE
catch_s3 : Function -> Any ! S3_Error
catch_s3 ~action =
    Panic.catch S3Exception action caught_panic->
        aws_error = caught_panic.payload.awsErrorDetails
        Error.throw (S3_Error.Error aws_error.errorMessage aws_error.errorCode)

## PRIVATE
make_client : (AWS_Credential | Nothing) -> S3Client
make_client credentials =
    pvdr = Auth.create_provider credentials
    S3Client.builder.credentialsProvider pvdr . build
