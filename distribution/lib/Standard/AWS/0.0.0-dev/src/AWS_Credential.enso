from Standard.Base import all

from Standard.Base.Metadata import Widget, make_single_choice

polyglot java import software.amazon.awssdk.profiles.ProfileFileSupplier

type AWS_Credential
    ## Access using IAM via an AWS profile.

       Arguments:
       - profile: AWS profile name (if empty uses default).
    @profile (make_single_choice AWS_Credential.profile_names)
    Profile profile:Text='default'

    ## Access using IAM via an AWS access key ID and secret access key.

       Arguments:
       - access_key: AWS access key ID.
       - secret_access_key: AWS secret access key.
    Key access_key:Text secret_access_key:Text

    ## Get a list of the available profiles
    profile_names : Vector Text
    profile_names =
        pvdr = ProfileFileSupplier.defaultSupplier.get
        nameMap = pvdr.profiles
        nameMap.keys

    ## PRIVATE
    default_widget : Widget
    default_widget =
        fqn = Meta.get_qualified_type_name AWS_Credential
        make_single_choice [["default", "Nothing"], ["by profile", fqn + ".Profile"], ["by key", fqn + ".Key"]]
