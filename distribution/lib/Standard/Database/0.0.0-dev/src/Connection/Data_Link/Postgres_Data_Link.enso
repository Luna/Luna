private

from Standard.Base import all
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
import Standard.Base.System.Input_Stream.Input_Stream
from Standard.Base.Enso_Cloud.Data_Link_Helpers import Data_Link_Source_Metadata, parse_secure_value
from Standard.Base.Enso_Cloud.Public_Utils import get_optional_field, get_required_field

import project.Connection.Connection_Options.Connection_Options
import project.Connection.Credentials.Credentials
import project.Connection.Postgres.Postgres
import project.Internal.Data_Link_Setup.DB_Data_Link_Type
import project.Internal.DB_Data_Link_Helpers

## PRIVATE
type Postgres_Data_Link
    ## PRIVATE
    Value details:Postgres source:Data_Link_Source_Metadata link_type:DB_Data_Link_Type

    ## PRIVATE
    parse json source:Data_Link_Source_Metadata -> Postgres_Data_Link =
        host = get_required_field "host" json expected_type=Text
        port = get_required_field "port" json expected_type=Integer
        db_name = get_required_field "database_name" json expected_type=Text
        schema = get_optional_field "schema" json if_missing="" expected_type=Text

        credentials_json = get_optional_field "credentials" json
        credentials = credentials_json.if_not_nothing <|
            username = get_required_field "username" credentials_json |> parse_secure_value
            password = get_required_field "password" credentials_json |> parse_secure_value
            Credentials.Username_And_Password username password

        details = Postgres.Server host=host port=port database=db_name schema=schema credentials=credentials
        link_type = DB_Data_Link_Type.from_js (get_optional_field "table" json)
        Postgres_Data_Link.Value details source link_type

    ## PRIVATE
    read self (format = Auto_Detect) (on_problems : Problem_Behavior) =
        _ = on_problems
        if format != Auto_Detect then Error.throw (Illegal_Argument.Error "Only Auto_Detect can be used with a Postgres Data Link, as it points to a database.") else
            default_options = DB_Data_Link_Helpers.data_link_connection_parameters self.source
            connection = self.details.connect default_options allow_data_links=False
            self.link_type.interpret connection
