from Standard.Base import all

import Standard.Database.Internal.JDBC_Connection
from Standard.Database.Internal.SQLite.SQLite_Connection import SQLite_Connection
import Standard.Database.Data.Dialect
import Standard.Database.Connection.Connection_Options
from Standard.Database.Connection.Connection import Connection

type SQLite
    ## Connect to a SQLite DB File or InMemory DB.

       Arguments:
       - location: Location of the SQLite database to connect to.
    SQLite_Data (location:(In_Memory|File|Text))

    ## Build the Connection resource.

       Arguments:
       - options: Overrides for the connection properties.
    connect : Connection_Options
    connect self options =
        properties = options.merge self.jdbc_properties
        jdbc_connection = JDBC_Connection.create self.jdbc_url properties
        SQLite_Connection (Connection jdbc_connection self.dialect)

    ## Provides the jdbc url for the connection.
    jdbc_url : Text
    jdbc_url self = case self.location of
        In_Memory -> "jdbc:sqlite::memory:"
        _ -> "jdbc:sqlite:" + ((File.new self.location).absolute.path.replace '\\' '/')

    ## Provides the properties for the connection.
    jdbc_properties : Vector
    jdbc_properties self = []

    ## Provides the dialect needed for creating SQL statements.
    dialect : Dialect
    dialect self = Dialect.sqlite

## Connect to an in-memory SQLite database.
type In_Memory
