private

import project.Data.Text.Text
import project.Data.Vector.Vector
import project.Enso_Cloud.Enso_File.Enso_File
import project.Enso_Cloud.Enso_User.Enso_User
import project.Error.Error
import project.Errors.Illegal_Argument.Illegal_Argument
import project.Errors.Unimplemented.Unimplemented
import project.Internal.Path_Helpers
import project.Nothing.Nothing
from project.Data.Boolean import Boolean, False, True
from project.Data.Text.Extensions import all

## PRIVATE
   UNSTABLE
   This is a helper for handling `enso://` paths.
type Enso_Path
    ## PRIVATE
    private Value (path_segments : Vector Text)

    ## PRIVATE
    parse (path : Text) -> Enso_Path =
        if path.starts_with Enso_Path.protocol_prefix . not then Error.throw (Illegal_Argument.Error "Invalid path - it should start with `enso://`.") else
            raw_segments = path.drop Enso_Path.protocol_prefix.length . split Enso_Path.delimiter
            if raw_segments.is_empty then Error.throw (Illegal_Argument.Error "Invalid path - it should contain at least one segment.") else
                segments = raw_segments.filter s-> s.is_empty.not
                normalized = normalize segments
                Enso_Path.Value normalized

    ## PRIVATE
    root = Enso_Path.Value []

    ## PRIVATE
    is_root self -> Boolean =
        self.path_segments.is_empty

    ## PRIVATE
    parent self -> Enso_Path =
        if self.is_root then Error.throw (Illegal_Argument.Error "Cannot get parent of the root directory.") else
            Enso_Path.Value (self.path_segments.drop (..Last 1))

    ## PRIVATE
    resolve self (subpath : Text) -> Enso_Path =
        new_segments = subpath.split Enso_Path.delimiter . filter (p-> p.is_empty.not)
        normalized_segments = normalize (self.path_segments + new_segments)
        Enso_Path.Value normalized_segments

    ## PRIVATE
    is_descendant_of self (other : Enso_Path) -> Boolean =
        if self.path_segments.length < other.path_segments.length then False else
            (self.path_segments.take other.path_segments.length) == other.path_segments

    ## PRIVATE
    to_text self -> Text =
        Enso_Path.protocol_prefix + self.path_segments.join Enso_Path.delimiter

    ## PRIVATE
    delimiter = "/"

    ## PRIVATE
    protocol_prefix = "enso://"

## PRIVATE
normalize segments =
    Path_Helpers.normalize_segments segments (x->x)
