import project.Data.Boolean.Boolean
import project.Data.Json.JS_Object
import project.Data.Json.Json
import project.Data.Numbers.Number
import project.Data.Text.Encoding.Encoding
import project.Data.Text.Text
import project.Data.Vector.Vector
import project.Network.HTTP.HTTP_Error.HTTP_Error
import project.Network.URI.URI
import project.Nothing.Nothing
import project.Runtime.Managed_Resource.Managed_Resource
import project.System.File.Existing_File_Behavior.Existing_File_Behavior
import project.System.File.File
import project.System.File.Input_Stream
import project.System.File.Write_Extensions
from project.Data.Text.Extensions import all
from project.System.File import close_stream

polyglot java import java.io.InputStream

## PRIVATE
type Response_Body
    ## Create a Response_Body.

       Arguments:
       - stream: The body of the response as an InputStream.
       - content_type: The content type of the response.
       - uri: The URI of the response.
       - bytes: The body as a Vector of bytes.
    new : InputStream -> Text -> URI -> Response_Body
    new stream content_type uri =
        resource = Managed_Resource.register stream close_stream
        enso_stream = Input_Stream.Value resource (HTTP_Error.handle_java_exceptions uri)
        Response_Body enso_stream content_type uri (stream.read_all_bytes)

    ## PRIVATE
    Value (stream:Input_Stream) (content_type:Text|Nothing) uri:URI ~bytes:Vector

    ## ALIAS parse as text
       GROUP Conversions
       Decodes the body to a Text value.
    @encoding Encoding.default_widget
    decode_as_text : Encoding -> Text
    decode_as_text self encoding=Encoding.utf_8 =
        Text.from_bytes self.bytes encoding

    ## ALIAS parse as json, parse json
       GROUP Conversions
       Decodes the body as JSON.

       > Example
         Convert a response from JSON.

             import Standard.Examples

             example_to_text = Examples.get_geo_data.decode_as_json
    @encoding Encoding.default_widget
    decode_as_json : Encoding -> JS_Object | Boolean | Number | Nothing | Text | Vector
    decode_as_json self encoding=Encoding.utf_8 =
        self.decode_as_text encoding . parse_json

    ## PRIVATE
       Convert response body to Text.
    to_text : Text
    to_text self = "Response_Body"

    ## GROUP Output
       Write response body to a File.

       Arguments:
       - file: The file to write the body to.
       - on_existing_file: Specifies how to proceed if the file already exists.

       > Examples
         Write the contents of the request body to a scratch file on disk. The
         file will be created if it does not exist, and will be overwritten if
         it does.

             import Standard.Examples

             example_to_file =
                Examples.get_geo_data.to_file Examples.scratch_file
    to_file : File | Text -> File
    to_file self file on_existing_file=Existing_File_Behavior.Backup =
        f = File.new file
        r = on_existing_file.write f stream->
            stream.write_stream self.stream
        r.if_not_error file
