from Standard.Base import all

polyglot java import org.enso.base.statistics.Regression
polyglot java import org.enso.base.statistics.FitError
polyglot java import java.lang.IllegalArgumentException

type Model
    ## Fit a line (y = A x + B) to the data with an optional fixed intercept.
    type Linear_Model (intercept:Number|Nothing=Nothing)

    ## Fit a exponential line (y = A exp(B x)) to the data with an optional fixed intercept.
    type Exponential_Model (intercept:Number|Nothing=Nothing)

    ## Fit a logarithmic line (y = A log x + B) to the data.
    type Logarithmic_Model

    ## Fit a power series (y = A x ^ B) to the data.
    type Power_Model

## Use Least Squares to fit a line to the data.
fit_least_squares : Vector -> Vector -> Model -> Fitted_Model ! Illegal_Argument_Error | Fit_Error
fit_least_squares known_xs known_ys model=Linear_Model =
    report_illegal caught_panic = Error.throw (Illegal_Argument_Error caught_panic.payload.cause.getMessage)
    handle_illegal = Panic.catch IllegalArgumentException handler=report_illegal

    handle_illegal <| Fit_Error.handle_Java_exception <| case model of
        Linear_Model intercept ->
            fitted = if intercept.is_nothing then Regression.fit_linear known_xs.to_array known_ys.to_array else
                Regression.fit_linear known_xs.to_array known_ys.to_array intercept
            Fitted_Linear_Model fitted.slope fitted.intercept fitted.rSquared
        Exponential_Model intercept ->
            log_ys = ln_series known_ys "Y-values"
            fitted = if intercept.is_nothing then Regression.fit_linear known_xs.to_array log_ys.to_array else
                Regression.fit_linear known_xs.to_array log_ys.to_array intercept.ln
            Fitted_Exponential_Model fitted.slope fitted.intercept fitted.rSquared
        Logarithmic_Model ->
            log_xs = ln_series known_xs "X-values"
            fitted = Regression.fit_linear log_xs.to_array known_ys.to_array
            Fitted_Logarithmic_Model fitted.slope fitted.intercept fitted.rSquared
        Power_Model ->
            log_xs = ln_series known_xs "X-values"
            log_ys = ln_series known_ys "Y-values"
            fitted = Regression.fit_linear log_xs.to_array log_ys.to_array
            Fitted_Power_Model fitted.slope fitted.intercept fitted.rSquared
        _ -> Error.throw (Illegal_Argument_Error "Unsupported model")

type Fitted_Model
    ## Fitted line (y = slope x + intercept).
    type Fitted_Linear_Model slope:Number intercept:Number r_squared:Number

    ## Fitted exponential line (y = A exp(B x)).
    type Fitted_Exponential_Model a:Number b:Number r_squared:Number

    ## Fitted logarithmic line (y = A log x + B).
    type Fitted_Logarithmic_Model a:Number b:Number r_squared:Number

    ## Fitted power series (y = A x ^ B).
    type Fitted_Power_Model a:Number b:Number r_squared:Number

    ## Display the fitted line.
    to_text : Text
    to_text = case self of
        Fitted_Linear_Model slope intercept _ -> slope.to_text + " * X + " + intercept.to_text
        Fitted_Exponential_Model a b _ -> a.to_text + " * (" + b.to_text + " * X).exp"
        Fitted_Logarithmic_Model a b _ -> a.to_text + " * X.ln + " + b.to_text
        Fitted_Power_Model a b _ -> a.to_text + " * X ^ " + b.to_text

    ## Use the model to predict a value.
    predict : Number -> Number
    predict x = case self of
        Fitted_Linear_Model slope intercept _ -> slope * x + intercept
        Fitted_Exponential_Model a b _ -> a * (b * x).exp
        Fitted_Logarithmic_Model a b _ -> a * x.ln + b
        Fitted_Power_Model a b _ -> a * x ^ b
            _ -> Error.throw (Illegal_Argument_Error "Unsupported model")

## PRIVATE

   Computes the natural log series as long as all values are positive.
ln_series : Vector -> Vector ! Illegal_Argument_Error
ln_series xs series_name="Values" =
    error_throw p = Error.throw p
    ln_with_panic x = if x.is_nothing then Nothing else
        if x <= 0 then Panic.throw (Illegal_Argument_Error (series_name + " must be positive.")) else x.ln
    Panic.catch Illegal_Argument_Error handler=error_throw <| xs.map ln_with_panic

## PRIVATE

   An error thrown when the linear regression cannot be computed.

   Arguments:
   - message: The error message.
type Fit_Error message

## PRIVATE

   Converts the `Fit_Error` to a human-readable representation.
Fit_Error.to_display_text : Text
Fit_Error.to_display_text = "Could not fit the model: " + self.message.to_text

## PRIVATE
Fit_Error.handle_java_exception =
    throw_fit_error caught_panic =
        cause = caught_panic.payload.cause
        Error.throw (Fit_Error cause.getMessage)
    Panic.catch FitError handler=throw_fit_error
