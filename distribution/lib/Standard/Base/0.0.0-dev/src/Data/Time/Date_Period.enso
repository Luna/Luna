from Standard.Base import all

polyglot java import org.enso.base.Time_Utils
polyglot java import org.enso.base.time.Date_Period_Utils
polyglot java import java.time.temporal.TemporalAdjuster
polyglot java import java.time.temporal.TemporalAdjusters

## Represents a period of time longer on the scale of days (longer than a day).
type Date_Period
    Year
    Quarter
    Month
    Week (start_of_week:Day_Of_Week = Day_Of_Week.Monday)

    ## PRIVATE
       This method could be replaced with matching on `Date_Period` supertype
       if/when that is supported.
    is_date_period : Boolean
    is_date_period self = True

    ## PRIVATE
    adjust_start : (Date | Date_Time) -> (Date | Date_Time)
    adjust_start self date =
        adjuster = case self of
            Year -> TemporalAdjusters.firstDayOfYear
            Quarter -> Date_Period_Utils.quarter_start
            Month -> TemporalAdjusters.firstDayOfMonth
            Week start_of_week -> TemporalAdjusters.previousOrSame start_of_week.to_java
        (Time_Utils.utils_for date).apply_adjuster date adjuster

    ## PRIVATE
    adjust_end : (Date | Date_Time) -> (Date | Date_Time)
    adjust_end self date =
        adjuster = case self of
            Year -> TemporalAdjusters.lastDayOfYear
            Quarter -> Date_Period_Utils.quarter_end
            Month -> TemporalAdjusters.lastDayOfMonth
            Week start_of_week -> Date_Period_Utils.end_of_week start_of_week.to_java
        (Time_Utils.utils_for date).apply_adjuster date adjuster
