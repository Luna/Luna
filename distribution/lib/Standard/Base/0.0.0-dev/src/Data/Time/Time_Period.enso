import project.Data.Time.Date_Time.Date_Time
import project.Data.Time.Duration.Duration
import project.Data.Time.Time_Of_Day.Time_Of_Day
from project.Data.Boolean import Boolean, False, True

polyglot java import java.time.temporal.ChronoUnit
polyglot java import java.time.temporal.TemporalUnit
polyglot java import org.enso.base.Time_Utils

## Represents a unit of time of a day or shorter.
type Time_Period
    ## Represents a time period of a single day, measured as 24 hours.

       ? Daylight Saving Time

         Note that due to DST changes, some days may be slightly longer or
         shorter. This is not reflected in the duration of this time period. For
         a calendar-oriented day period, use `Date_Period.Day` instead.
    Day

    ## Represents a time period of an hour.
    Hour

    ## Represents a time period of a minute.
    Minute

    ## Represents a time period of a second.
    Second

    ## Represents a time period of a millisecond.
    Millisecond

    ## Represents a time period of a microsecond.
    Microsecond

    ## Represents a time period of a nanosecond.
    Nanosecond

    ## PRIVATE
       Note that this needs to be used with care, since `ChronoUnit.DAYS` is
       actually closer to `Date_Period.Day` than the 24-hour interval here.
    to_java_unit : TemporalUnit
    to_java_unit self = case self of
        Time_Period.Day -> ChronoUnit.DAYS
        Time_Period.Hour -> ChronoUnit.HOURS
        Time_Period.Minute -> ChronoUnit.MINUTES
        Time_Period.Second -> ChronoUnit.SECONDS
        Time_Period.Millisecond -> ChronoUnit.MILLIS
        Time_Period.Nanosecond -> ChronoUnit.NANOS

    ## PRIVATE
    adjust_start : (Time_Of_Day | Date_Time) -> (Time_Of_Day | Date_Time)
    adjust_start self date =
        (Time_Utils.utils_for date).start_of_time_period date self.to_java_unit

    ## PRIVATE
    adjust_end : (Time_Of_Day | Date_Time) -> (Time_Of_Day | Date_Time)
    adjust_end self date =
        (Time_Utils.utils_for date).end_of_time_period date self.to_java_unit

    ## PRIVATE
    to_duration : Duration
    to_duration self = case self of
        Time_Period.Day -> Duration.new hours=24
        Time_Period.Hour -> Duration.new hours=1
        Time_Period.Minute -> Duration.new minutes=1
        Time_Period.Second -> Duration.new seconds=1
        Time_Period.Millisecond -> Duration.new milliseconds=1
        Time_Period.Nanosecond -> Duration.new nanoseconds=1
