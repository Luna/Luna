import project.Any.Any
import project.Data.Json.Json
import project.Data.Text.Encoding.Encoding
import project.Data.Text.Text
import project.Data.Vector.Vector
import project.Error.Error
import project.Error.File_Error.File_Error
import project.Error.Problem_Behavior.Problem_Behavior
import project.Error.Unimplemented.Unimplemented
import project.Network.HTTP.Response.Response
import project.Network.URI.URI
import project.Nothing.Nothing
import project.System.File.File

from project.Data.Boolean import Boolean, True, False

polyglot java import org.enso.base.file_format.FileFormatSPI

## PRIVATE
format_types : Vector
format_types = Vector.from_polyglot_array (FileFormatSPI.get_types False)

type Auto_Detect
    ## PRIVATE
       Implements the `File.read` for this `File_Format`
    read : File -> Problem_Behavior -> Any ! File_Error
    read self file on_problems =
        reader = Auto_Detect.get_format file
        if reader == Nothing then Error.throw (File_Error.Unsupported_Type file) else
            reader.read file on_problems

    ## PRIVATE
    get_format : File -> Any | Nothing
    get_format file =
        reader idx =
            if idx >= format_types.length then Nothing else
                format = format_types.at idx . for_file file
                if format.is_nothing.not then format else
                    @Tail_Call reader (idx + 1)
        reader 0

    ## PRIVATE
    get_web_parser : Text -> URI -> Any | Nothing
    get_web_parser mime_type uri =
        reader idx =
            if idx >= format_types.length then Nothing else
                format = format_types.at idx . for_web mime_type uri
                if format.is_nothing.not then format else
                    @Tail_Call reader (idx + 1)
        reader 0


type File_Format
    ## PRIVATE
       Implements the `File.read` for this `File_Format`
    read : File -> Problem_Behavior -> Any
    read _ _ = Unimplemented.throw "This is an interface only."

type Plain_Text_Format
    Plain_Text (encoding:Encoding=Encoding.utf_8)

    ## If the File_Format supports reading from the file, return a configured instance.
    for_file : File -> Plain_Text_Format | Nothing
    for_file file =
        case file.extension of
            ".txt" -> Plain_Text_Format.Plain_Text
            ".log" -> Plain_Text_Format.Plain_Text
            _ -> Nothing

    ## If the File_Format supports reading from the web response, return a configured instance.
    for_web : Text -> URI -> Plain_Text_Format | Nothing
    for_web mime_type _ =
        case mime_type.split ';' . first of
            "text/plain" -> Plain_Text_Format.Plain_Text
            _ -> Nothing

    ## PRIVATE
       Implements the `File.read` for this `File_Format`
    read : File -> Problem_Behavior -> Any
    read self file on_problems =
        file.read_text self.encoding on_problems

    ## PRIVATE
       Implements the `Data.parse` for this `File_Format`
    read_web : Response -> Problem_Behavior -> Any
    read_web self response on_problems =
        Text.from_bytes response.body.bytes self.encoding on_problems

type Bytes
    ## If the File_Format supports reading from the file, return a configured instance.
    for_file : File -> Bytes | Nothing
    for_file file =
        case file.extension of
            ".dat" -> Bytes
            _ -> Nothing

    ## If the File_Format supports reading from the web response, return a configured instance.
    for_web : Text -> URI -> Bytes | Nothing
    for_web _ _ = Nothing

    ## PRIVATE
       Implements the `File.read` for this `File_Format`
    read : File -> Problem_Behavior -> Any
    read self file _ =
        file.read_bytes

type JSON_File
    ## If the File_Format supports reading from the file, return a configured instance.
    for_file : File -> JSON_File | Nothing
    for_file file =
        case file.extension of
            ".json" -> JSON_File
            ".geojson" -> JSON_File
            _ -> Nothing

    ## If the File_Format supports reading from the web response, return a configured instance.
    for_web : Text -> URI -> JSON_File | Nothing
    for_web mime_type _ =
        case mime_type of
            "application/json" -> JSON_File
            _ -> Nothing

    ## PRIVATE
       Implements the `File.read` for this `File_Format`
    read : File -> Problem_Behavior -> Any
    read self file _ =
        text = file.read_text
        Json.parse text

## A setting to infer the default behaviour of some option.
type Infer
