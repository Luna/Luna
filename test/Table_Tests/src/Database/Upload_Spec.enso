from Standard.Base import all

from Standard.Table import all
from Standard.Table.Errors import Missing_Input_Columns

from Standard.Database import all
from Standard.Database.Errors import all
from Standard.Database.Internal.Result_Set import result_set_to_table
from Standard.Database.Extensions.Upload_Table import get_primary_key

from Standard.Test import Test, Test_Suite, Problems
import Standard.Test.Extensions

import project.Database.Helpers.Name_Generator

polyglot java import org.enso.table_test_helpers.ExplodingStorage
polyglot java import org.enso.table_test_helpers.ExplodingStoragePayload

main = Test_Suite.run_main <|
    spec (_ -> Database.connect (SQLite In_Memory)) "[SQLite] " persistent_connector=False

## PRIVATE
   Tests uploading tables.

   Arguments:
   - make_new_connection: a function that takes `Nothing` and returns a new
     connection.
   - prefix: a string that will be prepended to the test names.
   - persistent_connector: specifies if the database is persisted between
     connections. Should be `True` for all databases except SQLite in the
     `In_Memory` mode in which every re-connect creates a separate in-memory
     database, so features relying on persistence cannot really be tested.
spec make_new_connection prefix persistent_connector=True =
    connection = make_new_connection Nothing
    Test.group prefix+"Creating an empty table" <|
        Test.specify "should allow to specify the column names and types" <|
            Nothing

        Test.specify "should allow to inherit the structure of an existing in-memory table" <|
            Nothing

        Test.specify "should allow to inherit the structure of an existing Database table" <|
            Nothing

        Test.specify "should fail if the table already exists" <|
            Nothing

        Test.specify "should not fail if the table exists, if `if_not_exists=True`, even if the structure does not match" <|
            Nothing

        Test.specify "should drop the temporary table after the connection is closed" <|
            Nothing

        Test.specify "should preserve the regular table after the connection is closed" <|
            Nothing

    Test.group prefix+"Uploading an in-memory Table" <|
        in_memory_table = Table.new [["X", [1, 2, 3]], ["Y", ['a', 'b', 'c']]]
        Test.specify "should include the created table in the tables directory" <|
            db_table = in_memory_table.select_into_database_table connection (Name_Generator.random_name "permanent_table 1") temporary=False
            Panic.with_finalizer (connection.drop_table db_table.name) <|
                db_table.at "X" . to_vector . should_equal [1, 2, 3]

                connection.tables.at "Name" . to_vector . should_contain db_table.name
                connection.query db_table.name . at "X" . to_vector . should_equal [1, 2, 3]

        Test.specify "should include the temporary table in the tables directory" <|
            db_table = in_memory_table.select_into_database_table connection (Name_Generator.random_name "temporary_table 1") temporary=True
            db_table.at "X" . to_vector . should_equal [1, 2, 3]
            connection.tables.at "Name" . to_vector . should_contain db_table.name
            connection.query db_table.name . at "X" . to_vector . should_equal [1, 2, 3]

        if persistent_connector then
            Test.specify "should drop the temporary table after the connection is closed" <|
                tmp_connection = make_new_connection Nothing
                db_table = in_memory_table.select_into_database_table tmp_connection (Name_Generator.random_name "temporary_table 2") temporary=True
                name = db_table.name
                tmp_connection.query (SQL_Query.Table_Name name) . at "X" . to_vector . should_equal [1, 2, 3]
                tmp_connection.close
                connection.query (SQL_Query.Table_Name name) . should_fail_with Table_Not_Found

            Test.specify "should preserve the regular table after the connection is closed" <|
                tmp_connection = make_new_connection Nothing
                db_table = in_memory_table.select_into_database_table tmp_connection (Name_Generator.random_name "permanent_table 1") temporary=False
                name = db_table.name
                Panic.with_finalizer (connection.drop_table name) <|
                    tmp_connection.query (SQL_Query.Table_Name name) . at "X" . to_vector . should_equal [1, 2, 3]
                    tmp_connection.close
                    connection.query (SQL_Query.Table_Name name) . at "X" . to_vector . should_equal [1, 2, 3]

        Test.specify "should rollback the table creation if select_into_database_table fails" <|
            normal_column = Column.from_vector "Y" ((100+0).up_to (100+1000)).to_vector
            exploding_column = make_mock_column "X" (0.up_to 1000).to_vector 512
            exploding_table = Table.new [normal_column, exploding_column]
            name = Name_Generator.random_name "rolling-back-table"
            connection.query (SQL_Query.Table_Name name) . should_fail_with Table_Not_Found
            Test.expect_panic_with matcher=ExplodingStoragePayload <|
                exploding_table.select_into_database_table connection name temporary=False primary_key=Nothing
            connection.query (SQL_Query.Table_Name name) . should_fail_with Table_Not_Found

        Test.specify "should set a primary key for the table" <|
            t1 = Table.new [["X", [1, 2, 3]], ["Y", ['a', 'b', 'c']], ["Z", [1.0, 2.0, 3.0]]]
            db_table_1 = t1.select_into_database_table connection (Name_Generator.random_name "primary-key-1") primary_key=["Y", "X"]
            Panic.with_finalizer (connection.drop_table db_table_1.name) <|
                db_table_1.at "X" . to_vector . should_equal [1, 2, 3]
                get_primary_key connection db_table_1.name . should_equal ["Y", "X"]

            db_table_2 = t1.select_into_database_table connection (Name_Generator.random_name "primary-key-2")
            Panic.with_finalizer (connection.drop_table db_table_2.name) <|
                db_table_2.at "X" . to_vector . should_equal [1, 2, 3]
                get_primary_key connection db_table_2.name . should_equal ["X"]

            db_table_3 = t1.select_into_database_table connection (Name_Generator.random_name "primary-key-3") primary_key=Nothing
            Panic.with_finalizer (connection.drop_table db_table_3.name) <|
                db_table_3.at "X" . to_vector . should_equal [1, 2, 3]
                get_primary_key connection db_table_3.name . should_equal Nothing

        Test.specify "should ensure that primary key columns are valid" <|
            r1 = in_memory_table.select_into_database_table connection (Name_Generator.random_name "primary-key-4") primary_key=["X", "nonexistent"]
            r1.should_fail_with Missing_Input_Columns

            db_table_2 = in_memory_table.select_into_database_table connection (Name_Generator.random_name "primary-key-5") primary_key=["X", 0]
            Panic.with_finalizer (connection.drop_table db_table_2.name) <|
                get_primary_key connection db_table_2.name . should_equal ["X"]

        Test.specify "should fail if the primary key is not unique" <|
            t1 = Table.new [["X", [1, 2, 1]], ["Y", ['b', 'b', 'a']]]
            r1 = t1.select_into_database_table connection (Name_Generator.random_name "primary-key-6") temporary=True primary_key=["X"]
            r1.should_fail_with Non_Unique_Primary_Key
            e1 = r1.catch
            e1.clashing_primary_key . should_equal [1]
            e1.clashing_example_row_count . should_equal 2
            e1.to_display_text . should_equal "The primary key [X] is not unique. The key [1] corresponds to 2 rows."

            r2 = t1.select_into_database_table connection (Name_Generator.random_name "primary-key-6") temporary=True primary_key=["Y"]
            r2.should_fail_with Non_Unique_Primary_Key
            r2.catch . clashing_primary_key . should_equal ['b']

            r3 = t1.select_into_database_table connection (Name_Generator.random_name "primary-key-7") temporary=True primary_key=["X", "Y"]
            r3.at "X" . to_vector . should_equal [1, 2, 1]

            t2 = Table.new [["X", [1, 2, 1]], ["Y", ['a', 'b', 'a']]]
            r4 = t2.select_into_database_table connection (Name_Generator.random_name "primary-key-7") temporary=True primary_key=["X", "Y"]
            r4.should_fail_with Non_Unique_Primary_Key
            r4.catch . clashing_primary_key . should_equal [1, 'a']

    Test.group prefix+"Persisting a Database Table (query)" <|
        Test.specify "should be able to create a persistent copy of a DB table" <|
            t = Table.new [["X", [1, 2, 3]], ["Y", ['a', 'b', 'c']], ["Z", [1.0, 2.0, 3.0]]]
            tmp_connection = make_new_connection Nothing
            db_table = t.select_into_database_table tmp_connection (Name_Generator.random_name "source-table") temporary=True

            copied_table = db_table.select_into_database_table tmp_connection (Name_Generator.random_name "copied-table") temporary=False
            name = copied_table.name
            Panic.with_finalizer (connection.drop_table name) <|
                copied_table.at "X" . value_type . is_integer . should_be_true
                copied_table.at "Y" . value_type . is_text . should_be_true
                copied_table.at "Z" . value_type . is_floating_point . should_be_true

                tmp_connection.query name . at "X" . to_vector . should_equal [1, 2, 3]
                tmp_connection.close

                if persistent_connector then
                    connection.query name . at "X" . to_vector . should_equal [1, 2, 3]

        Test.specify "should be able to persist a complex query with generated columns, joins etc." <|
            t1 = Table.new [["X", [1, 1, 2]], ["Y", [1, 2, 3]]]

            db_table_1 = t1.select_into_database_table connection (Name_Generator.random_name "source-table-1") temporary=True primary_key=Nothing

            db_table_2 = db_table_1.set "[Y] + 100 * [X]" "C1" . set '"constant_text"' "C2"
            db_table_3 = db_table_1.aggregate [Aggregate_Column.Group_By "X", Aggregate_Column.Sum "[Y]*[Y]" "C3"] . set "[X] + 1" "X"

            db_table_4 = db_table_2.join db_table_3 join_kind=Join_Kind.Left_Outer

            copied_table = db_table_4.select_into_database_table connection (Name_Generator.random_name "copied-table") temporary=True primary_key=Nothing
            copied_table.column_names . should_equal ["X", "Y", "C1", "C2", "Right X", "C3"]
            copied_table.at "X" . to_vector . should_equal [1, 1, 2]
            copied_table.at "C1" . to_vector . should_equal [101, 102, 203]
            copied_table.at "C2" . to_vector . should_equal ["constant_text", "constant_text", "constant_text"]
            copied_table.at "Right X" . to_vector . should_equal [Nothing, Nothing, 2]
            copied_table.at "C3" . to_vector . should_equal [Nothing, Nothing, 5]

            # We check that this is indeed querying a simple DB table and not a complex query like `db_table_4` would be,
            sql = copied_table.to_sql.prepare.first
            Test.with_clue "sql="+sql <|
                sql.contains "WHERE" . should_be_false
                sql.contains "JOIN" . should_be_false
                sql.contains "GROUP" . should_be_false

        Test.specify "should be able to create a temporary copy of a query" <|
            tmp_connection = make_new_connection Nothing
            t = Table.new [["X", [1, 2, 3]], ["Y", [4, 5, 6]]]
            db_table = t.select_into_database_table tmp_connection (Name_Generator.random_name "source-table") temporary=True
            db_table_2 = db_table.set "[X] + 100 * [Y]" "computed"

            copied_table = db_table_2.select_into_database_table tmp_connection (Name_Generator.random_name "copied-table") temporary=True
            name = copied_table.name

            copied_table_accessed = tmp_connection.query name
            copied_table_accessed.column_names . should_equal ["X", "Y", "computed"]
            copied_table_accessed.at "computed" . to_vector . should_equal [401, 502, 603]
            tmp_connection.close

            connection.query name . should_fail_with Table_Not_Found

        Test.specify "should be able to specify a primary key" <|
            t = Table.new [["X", [1, 2, 3]], ["Y", ['a', 'b', 'c']]]
            db_table = t.select_into_database_table connection (Name_Generator.random_name "source-table") temporary=True
            db_table_2 = db_table.select_into_database_table connection (Name_Generator.random_name "copied-table") primary_key=["X"]
            Panic.with_finalizer (connection.drop_table db_table_2.name) <|
                get_primary_key connection db_table_2.name . should_equal ["X"]

        Test.specify "should ensure that primary key columns are valid" <|
            t = Table.new [["X", [1, 2, 3]], ["Y", ['a', 'b', 'c']]]
            db_table = t.select_into_database_table connection (Name_Generator.random_name "source-table") temporary=True
            r1 = db_table.select_into_database_table connection (Name_Generator.random_name "copied-table") temporary=True primary_key=["nonexistent"]
            r1.should_fail_with Missing_Input_Columns

        Test.specify "should fail when the primary key is not unique" <|
            t = Table.new [["X", [1, 2, 1]], ["Y", ['b', 'b', 'a']]]
            db_table = t.select_into_database_table connection (Name_Generator.random_name "source-table") temporary=True primary_key=Nothing
            Problems.assume_no_problems db_table

            r1 = db_table.select_into_database_table connection (Name_Generator.random_name "copied-table") temporary=True primary_key=["X"]
            r1.should_fail_with Non_Unique_Primary_Key
            e1 = r1.catch
            e1.clashing_primary_key . should_equal [1]
            e1.clashing_example_row_count . should_equal 2

            t2 = Table.new [["X", [1, 3, 1, 2, 3, 2, 2, 2, 0]], ["Y", ['a', 'b', 'c', 'd', 'e', 'f', 'g', 'h', 'i']]]
            db_table_2 = t2.select_into_database_table connection (Name_Generator.random_name "source-table-2") temporary=True primary_key=Nothing
            Problems.assume_no_problems db_table_2

            r2 = db_table_2.select_into_database_table connection (Name_Generator.random_name "copied-table-2") temporary=True primary_key=["X"]
            r2.should_fail_with Non_Unique_Primary_Key
            e2 = r2.catch
            e2.clashing_primary_key.length . should_equal 1
            x = e2.clashing_primary_key.first
            [1, 2, 3].should_contain x
            counts = Map.from_vector [[1, 2], [2, 4], [3, 2]]
            e2.clashing_example_row_count . should_equal (counts.at x)

        Test.specify "will not allow to upload tables across connections" <|
            t = Table.new [["X", [1, 2, 1]], ["Y", ['b', 'b', 'a']]]
            db_table = t.select_into_database_table connection (Name_Generator.random_name "source-table") temporary=True primary_key=Nothing

            connection_2 = make_new_connection Nothing
            r1 = db_table.select_into_database_table connection_2 (Name_Generator.random_name "copied-table") temporary=True primary_key=Nothing
            r1.should_fail_with Unsupported_Database_Operation
            r1.catch.message . should_contain "same connection"

    test_table_append table_builder =
        Test.specify "should be able to append new rows to a table" <|
            Nothing

        Test.specify "should error if new rows clash with existing ones and mode is Insert, target table should remain unchanged" <|
            Nothing

        Test.specify "should use the target table primary key for the key by default" <|
            Nothing

        Test.specify "should be able to update existing rows in a table" <|
            Nothing

        Test.specify "should upsert by default (update existing rows, insert new rows)" <|
            Nothing

        Test.specify "should allow to align an existing table with a source (upsert + delete rows missing from source)" <|
            Nothing

        Test.specify "should allow to use a transformed table, with computed fields, as a source" <|
            Nothing

        Test.specify "should be able to detect the primary key of a table for row correlation" <|
            Nothing

        Test.specify "should allow specifying no key in Insert mode" <|
            Nothing

        Test.specify "should fail if no key is specified in other modes" <|
            Nothing

        Test.specify "should fail if the key is not unique in the unique table" <|
            Nothing

        Test.specify "should fail if the key causes update of multiple values (it's not unique in the target table)" <|
            Nothing

        Test.specify "should fail if the source table contains columns not present in the target (data loss)" <|
            Nothing

        Test.specify "by default should keep columns missing from source as-is and use defaults when inserting" <|
            Nothing

        Test.specify "should fail if the source table is missing some columns and the column in the target has no default value" <|
            Nothing

        Test.specify "should fail if the source table is missing some columns, if asked to" <|
            Nothing

        Test.specify "should fail if some of key_columns do not exist in either table" <|
            Nothing

        Test.specify "by default, should fail if the target table does not exist" <|
            Nothing

        Test.specify "should allow to create the target table if it does not exist, but make it temporary and attach a warning" <|
            Nothing

        Test.specify "should report a meaningful error when the primary key could not be determined" <|
            # TODO temporary (only SQLite?)
            Nothing

            # TODO non-existent target
            Nothing

        Test.specify "should fail if types of columns are not compatible" <|
            # TODO how do we define compatibility? I assume we allow type widening, but not narrowing
            Nothing


    Test.group "Appending an in-memory table to a Database table" <|
        table_builder = Table.new
        test_table_append table_builder

    Test.group "Appending a Database table to a Database table" <|
        table_builder args =
            in_memory_table = Table.new args
            in_memory_table.select_into_database_table connection (Name_Generator.random_name "source-table") temporary=True
        test_table_append table_builder


## PRIVATE
   Creates a mock column containing `values`.

   If `exploding_index` is accessed, an exception will be thrown.
make_mock_column name values exploding_index =
    storage = ExplodingStorage.new values.to_array exploding_index
    Column.from_storage name storage
