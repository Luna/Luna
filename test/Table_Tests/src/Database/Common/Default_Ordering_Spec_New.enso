from Standard.Base import all
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
from Standard.Base.Runtime import assert

from Standard.Table import Table, Sort_Column, Aggregate_Column
from Standard.Table.Errors import all

from Standard.Database import all
from Standard.Database.Errors import all

from Standard.Test_New import all
import Standard.Test_New.Suite.Suite_Builder

import project.Util
import project.Database.Helpers.Name_Generator

type Data
    Impl ~db_table_without_key ~db_table_with_key

    create connection_provider =
        src_table = Table.new [["X", [1, 2, 3]], ["Y", [30, 20, 10]]]
        Data.Impl (Data.create_db_table_without_key src_table connection_provider) (Data.create_db_table_with_key src_table connection_provider)

    create_db_table_without_key src_table connection_provider =
        src_table.select_into_database_table connection_provider.connection (Name_Generator.random_name "default-ordering-1") temporary=True primary_key=Nothing

    create_db_table_with_key src_table connection_provider =
        src_table.select_into_database_table connection_provider.connection (Name_Generator.random_name "default-ordering-1") temporary=True primary_key=["X"]


## Adds test specifications for default ordering to the given `suite_builder`. Adds it as
   group with the given `prefix` as its name prefix.

   Arguments:
   - suite_builder: A Suite_Builder in which a new group will be created
   - connection_provider: An atom with `connection` method that returns a database connection.
                          Preferably, this method should be lazy. It must contain `teardown` method as well.
add_default_ordering_specs (suite_builder : Suite_Builder) (prefix : Text) (connection_provider : Any) =
    expected_methods_in_provider = ["connection", "teardown"]
    provider_type = Meta.type_of connection_provider
    is_correct_provider = expected_methods_in_provider.all expected_method->
        provider_methods = Meta.get_type_methods provider_type
        provider_methods.contains expected_method
    if is_correct_provider.not then
        # Print the message to stderr to make sure that it is not abbreviated in the Panic message
        msg = "connection_provider must contain all these methods: " + expected_methods_in_provider.to_text + ". Actual type is " + provider_type.to_text
        IO.print_err msg
        Panic.throw <| Illegal_Argument.Error "connection_provider"

    group_name = prefix + "Table.default_ordering"

    suite_builder.group group_name group_builder->
        data = Data.create connection_provider

        group_builder.teardown <|
            connection_provider.teardown

        group_builder.specify "will return Nothing if no primary key is defined" <|
            data.db_table_without_key.default_ordering . should_equal Nothing

        group_builder.specify "will return the key for a table with a primary key" <|
            v1 = data.db_table_with_key.default_ordering
            v1.length . should_equal 1
            v1.first.expression.name . should_equal "X"
            v1.first.direction . should_equal Sort_Direction.Ascending

            t2 = data.db_table_with_key.set "10 - [X]" "X"
            v2 = t2.default_ordering
            v2.length . should_equal 1
            v2.first.expression.name . should_equal "X"

        group_builder.specify "will return Nothing for composite tables (join, aggregate)"
            data.db_table_with_key.join data.db_table_with_key . default_ordering . should_equal Nothing
            data.db_table_with_key.aggregate [Aggregate_Column.Group_By "X"] . default_ordering . should_equal Nothing

        group_builder.specify "will return the ordering determined by order_by" <|
            v1 = data.db_table_with_key.order_by ["Y", Sort_Column.Name "X" Sort_Direction.Descending] . default_ordering
            v1.length . should_equal 2
            v1.first.expression.name . should_equal "Y"
            v1.first.direction . should_equal Sort_Direction.Ascending
            v1.second.expression.name . should_equal "X"
            v1.second.direction . should_equal Sort_Direction.Descending

            v2 = data.db_table_without_key.order_by ["Y"] . default_ordering
            v2.length . should_equal 1
            v2.first.expression.name . should_equal "Y"
            v2.first.direction . should_equal Sort_Direction.Ascending

