from Standard.Base import all

import Standard.Base.Data.Locale.Locale
import Standard.Base.Data.Time.Date.Date
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
import Standard.Test.Extensions

from Standard.Table import Column, Value_Type
from Standard.Test import Test, Test_Suite, Problems
from project.Util import all

spec =
    Test.group "Column.format, with format string" <|
        Test.specify "Date column" <|
            input = Column.from_vector "dates" [Date.new 2020 12 21, Date.new 2023 4 25]
            expected = Column.from_vector "dates" ["20201221", "20230425"]
            actual = input.format "yyyyMMdd"
            actual . should_equal expected

        Test.specify "Date with locale" <|
            input = Column.from_vector "dates" [Date.new 2020 6 21, Date.new 2023 4 25]
            expected_default = Column.from_vector "dates" ["21. Jun 2020", "25. Apr 2023"]
            expected_gb = Column.from_vector "dates" ["21. Jun 2020", "25. Apr 2023"]
            expected_fr = Column.from_vector "dates" ["21. juin 2020", "25. avril 2023"]
            input.format "d. MMMM yyyy" . should_equal expected_default
            input.format "d. MMMM yyyy" (Locale.default) . should_equal expected_default
            input.format "d. MMMM yyyy" (Locale.new "gb") . should_equal expected_gb
            input.format "d. MMMM yyyy" (Locale.new "fr") . should_equal expected_fr

        Test.specify "Bad format" <|
            input = Column.from_vector "dates" [Date.new 2020 6 21, Date.new 2023 4 25]
            expected = Column.from_vector "dates" [Nothing, Nothing]
            action = input.format "DDDDD" on_problems=_
            tester = t-> t.should_equal expected
            problems = [Illegal_Argument.Error 'Too many pattern letters: D']
            Problems.test_problem_handling action problems tester

    Test.group "Column.format, with format Column" <|
        Test.specify "Date column" <|
            input = Column.from_vector "dates" [Date.new 2020 12 21, Date.new 2023 4 25]
            formats = Column.from_vector "formats" ["yyyyMMdd", "dd-MM-yyyy"]
            expected = Column.from_vector "dates" ["20201221", "25-04-2023"]
            actual = input.format formats
            actual . should_equal expected

        Test.specify "Date with locale" <|
            input = Column.from_vector "dates" [Date.new 2020 6 21, Date.new 2023 4 25]
            formats = Column.from_vector "formats" ["d. MMMM yyyy", "d-MMMM-yyyy"]
            expected = Column.from_vector "dates" ["21. juin 2020", "25-avril-2023"]
            input.format formats (Locale.new "fr") . should_equal expected

        Test.specify "Bad format" <|
            input = Column.from_vector "dates" [Date.new 2020 6 21, Date.new 2023 4 25]
            formats = Column.from_vector "formats" ["yyyyMMdd", "DDDDD"]
            expected = Column.from_vector "dates" ['20200621', Nothing]
            action = input.format formats on_problems=_
            tester = t-> t.should_equal expected
            problems = [Illegal_Argument.Error 'Too many pattern letters: D']
            Problems.test_problem_handling action problems tester

        Test.specify "column length  mismatch" <|
            input = Column.from_vector "dates" [Date.new 2020 6 21, Date.new 2023 4 25]
            formats = Column.from_vector "formats" ["yyyyMMdd", "DDDDD", "w"]
            input.format formats . should_fail_with Illegal_Argument

main = Test_Suite.run_main spec
