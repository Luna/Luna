from Base import all

import Base.Test
import Base.Time
import Base.Time.Date
import Base.Time.Duration
import Base.Time.Time_Of_Day
import Base.Time.Zone

spec =
    describe "Time_Of_Day" <|
        it "should create local time" <|
            time = Time_Of_Day.new 1 0 0
            time . get_hour . should_equal 1
            time . get_minute . should_equal 0
            time . get_second . should_equal 0
            time . to_seconds . should_equal 3600
        it "should format local time using provided pattern" <|
            text = Time_Of_Day.new 12 20 44 . format "HHmmss"
            text . should_equal "122044"
        it "should format local time using default pattern" <|
            text = Time_Of_Day.new 12 20 44 . to_text
            text . should_equal "12:20:44"
        it "should parse default time format" <|
            text = Time_Of_Day.new 12 20 44 . to_text
            time = Time_Of_Day.parse text
            time.to_seconds . should_equal 44444
        it "should parse local time" <|
            time = Time_Of_Day.parse "10:00:00"
            time.to_seconds . should_equal 36000
        it "should convert to time" <|
            time = Time_Of_Day.new 1 0 0 . to_time (Date.new 2000 12 21) Zone.utc
            time . get_year . should_equal 2000
            time . get_month . should_equal 12
            time . get_day . should_equal 21
            time . get_hour . should_equal 1
            time . get_minute . should_equal 0
            time . get_second . should_equal 0
            time . get_nanosecond . should_equal 0
            time . get_zone . get_id . should_equal Zone.utc.get_id
        it "should add time-based interval" <|
            time = Time_Of_Day.new.plus 1.minute
            time . to_seconds . should_equal 60
        it "should subtract time-based interval" <|
            time = Time_Of_Day.new.minus 1.minute
            time . to_seconds . should_equal 86340
        it "should throw error when adding date-based interval" <|
            case Time_Of_Day.new.plus 1.day . catch (x -> x) of
                Time.Error message ->
                    message . should_equal "Time_Of_Day does not support date intervals"
                result ->
                    Test.fail ("Unexpected result: " + result.to_text)
        it "should throw error when subtracting date-based interval" <|
            case Time_Of_Day.new.minus (1.day . minus 1.minute) . catch (x -> x) of
                Time.Error message ->
                    message . should_equal "Time_Of_Day does not support date intervals"
                result ->
                    Test.fail ("Unexpected result: " + result.to_text)
