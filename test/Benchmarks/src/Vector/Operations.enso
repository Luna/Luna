from Standard.Base import all
import Standard.Base.Runtime.State
import Standard.Base

from Standard.Test import Bench, Phase_Conf

import project.Vector.Utils

polyglot java import java.util.Random as Java_Random

# The Benchmarks ==============================================================

type Setup
    Value vector_size random_vec random_vec_2 random_gen

    setup =
        vector_size = 1000000
        random_vec = Utils.make_random_vec vector_size
        random_vec_2 = Utils.make_random_vec 100000
        random_gen = Java_Random.new 123456
        Setup.Value vector_size random_vec random_vec_2 random_gen

    stateful_fun x =
       s = State.get Number
       State.put s+x


options = Bench.options . set_warmup (Phase_Conf.Secs 2) . set_measure (Phase_Conf.Secs 2) . set_setup Setup


collect_benches = Bench.build builder->
    builder.group "Vector_Operations" options group_builder->
        group_builder.specify "New_Vector" arg->
            Base.Vector.new arg.vector_size i->i

        group_builder.specify "New_Constant" arg->
            Base.Vector.new arg.vector_size _->42

        group_builder.specify "New_Random" arg->
            Base.Vector.new arg.vector_size _->arg.random_gen.nextLong

        group_builder.specify "Fill_Constant" arg->
            Base.Vector.fill arg.vector_size 42

        group_builder.specify "Fill_Random_constant" arg->
            Base.Vector.fill arg.vector_size arg.random_gen.nextLong

        group_builder.specify "Append_Single" arg->
            arg.random_vec + [1]

        group_builder.specify "Append_Large" arg->
            arg.random_vec + arg.random_vec_2

        group_builder.specify "Max" arg->
            arg.random_vec.reduce (Math.max)

        group_builder.specify "Sum" arg->
            arg.random_vec.reduce (+)

        group_builder.specify "Drop_First_20_and_Sum" arg->
            (arg.random_vec.drop (First 20)).reduce (+)

        group_builder.specify "Drop_Last_20_and_Sum" arg->
            (arg.random_vec.drop (Last 20)).reduce (+)

        group_builder.specify "Filter" arg->
            arg.random_vec.filter (x -> x % 3 == 1)

        group_builder.specify "Filter_With_Index" arg->
            arg.random_vec.filter_with_index (i-> x-> (i+x) % 3 == 1)

        group_builder.specify "Max_Stats" arg->
            arg.random_vec.compute Statistic.Maximum

        group_builder.specify "Sum_Stats" arg->
            arg.random_vec.compute Statistic.Sum

        group_builder.specify "Variance_Stats" arg->
            arg.random_vec.compute Statistic.Variance

        group_builder.specify "Map_and_Filter" arg->
            arg.random_vec . map (x -> x +  arg.random_gen.nextLong) . filter (x -> x % 3 == 1)

        group_builder.specify "Partition" arg->
            arg.random_vec.partition (x -> x % 3 == 1)

        group_builder.specify "Partition_With_Index" arg->
            arg.random_vec.partition_with_index (i-> x-> (i+x) % 3 == 1)

        group_builder.specify "Each" arg->
            State.run Number 0 <| arg.random_vec.each arg.stateful_fun


main = collect_benches . run_main
