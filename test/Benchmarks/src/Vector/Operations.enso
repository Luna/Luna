from Standard.Base import all
import Standard.Base.Runtime.State
import Standard.Base

from Standard.Test import Bench, Phase_Conf

import project.Vector.Utils

polyglot java import java.util.Random as Java_Random

## Bench Utilities ============================================================

vector_size = 1000000
warmup = Phase_Conf.Secs 2
measure = Phase_Conf.Secs 2

# The Benchmarks ==============================================================

collect_benches group_builder =
    random_vec = Utils.make_random_vec vector_size
    random_vec_2 = Utils.make_random_vec 100000
    random_gen = Java_Random.new 123456

    bench_measure ~act name = group_builder.specify name act

    bench_measure (Base.Vector.new vector_size i->i) "New_Vector"
    bench_measure (Base.Vector.new vector_size _->42) "New_Constant"
    bench_measure (Base.Vector.new vector_size _->random_gen.nextLong) "New_Random"
    bench_measure (Base.Vector.fill vector_size 42) "Fill_Constant"
    bench_measure (Base.Vector.fill vector_size random_gen.nextLong) "Fill_Random_constant"
    bench_measure (random_vec + [1]) "Append_Single"
    bench_measure (random_vec + random_vec_2) "Append_Large"
    bench_measure (random_vec.reduce (Math.max)) "Max"
    bench_measure (random_vec.reduce (+)) "Sum"
    bench_measure ((random_vec.drop (First 20)).reduce (+)) "Drop_First_20_and_Sum"
    bench_measure ((random_vec.drop (Last 20)).reduce (+)) "Drop_Last_20_and_Sum"
    bench_measure (random_vec.filter (x -> x % 3 == 1)) "Filter"
    bench_measure (random_vec.filter_with_index (i-> x-> (i+x) % 3 == 1)) "Filter_With_Index"
    bench_measure (random_vec.compute Statistic.Maximum) "Max_Stats"
    bench_measure (random_vec.compute Statistic.Sum) "Sum_Stats"
    bench_measure (random_vec.compute Statistic.Variance) "Variance_Stats"

    bench_measure (random_vec . map (x -> x +  random_gen.nextLong) . filter (x -> x % 3 == 1)) "Map_and_Filter"
    bench_measure (random_vec.partition (x -> x % 3 == 1)) "Partition"
    bench_measure (random_vec.partition_with_index (i-> x-> (i+x) % 3 == 1)) "Partition_With_Index"

    stateful_fun x =
       s = State.get Number
       State.put s+x
    bench_measure (State.run Number 0 <| random_vec.each stateful_fun) "Each"

options = Bench.options . set_warmup warmup . set_measure measure

all = Bench.build builder->
    builder.group "Vector_Operations" options group_builder->
        collect_benches group_builder

main = all . run_main
