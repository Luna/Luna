from Standard.Base import all
import Standard.Base.Runtime.Ref.Ref

from Standard.Test import Bench, Phase_Conf

import project.Vector.Utils

polyglot java import java.util.Random as Java_Random
polyglot java import org.enso.base.Time_Utils


## Bench Utilities ============================================================

make_sorted_ascending_vec : Integer -> Vector
make_sorted_ascending_vec n = 0.up_to n+1 . to_vector


make_partially_sorted_vec : Integer -> Vector
make_partially_sorted_vec n =
    random_gen = Java_Random.new n
    direction = Ref.new Sort_Direction.Ascending
    last_num = Ref.new 0
    run_length = Ref.new 0
    Vector.fill n <|
        case (run_length.get) == 0 of
            True ->
                new_direction = if random_gen.nextDouble > 0 then Sort_Direction.Ascending else
                    Sort_Direction.Descending
                direction.put new_direction
                run_length.put ((random_gen.nextLong % (n / 10).floor) - 1)
                num = random_gen.nextInt
                last_num.put num
                num
            False ->
                change = random_gen.nextInt.abs % n
                num = case direction.get of
                    Sort_Direction.Ascending ->
                        num = last_num.get + change
                        last_num.put num
                        num
                    Sort_Direction.Descending ->
                        num = (last_num.get) - change
                        last_num.put num
                        num
                run_length.put (run_length.get - 1)
                num

type Int
    Value v

    identity self = self


# The Benchmarks ==============================================================

options = Bench.options

collect_benches = Bench.build builder->
    vector_size = 1000000
    sorted_vec = make_sorted_ascending_vec vector_size
    partially_sorted_vec = make_partially_sorted_vec vector_size
    random_vec = Utils.make_random_vec vector_size
    random_vec_wrapped = random_vec.map (v -> Int.Value v)
    projection = x -> x % 10
    comparator = l -> r -> Ordering.compare l r

    builder.group "Vector_Sort" options group_builder->
        group_builder.specify "Already_Sorted" <|
            sorted_vec.sort

        group_builder.specify "Sorted_in_Opposite_Order" <|
            sorted_vec.sort Sort_Direction.Descending

        group_builder.specify "Sorted_Runs_Ascending" <|
            partially_sorted_vec.sort

        group_builder.specify "Sorted_Runs_Descending" <|
            partially_sorted_vec.sort Sort_Direction.Descending

        group_builder.specify "Random_Elements_Ascending" <|
            random_vec.sort

        group_builder.specify "Random_Elements_Descending" <|
            random_vec.sort Sort_Direction.Descending

        group_builder.specify "Sorting_with_a_Custom_Projection" <|
            random_vec.sort on=projection

        group_builder.specify "Sorting_with_an_identity_function" <|
            random_vec_wrapped.sort on=(_.identity)

        group_builder.specify "Sorting_with_an_unresolved_identity_function" <|
            random_vec_wrapped.sort on=(.identity)

        group_builder.specify "Sorting_with_the_Default_Ordered_Comparator" <|
            random_vec.sort by=comparator


main = collect_benches . run_main
