from Standard.Base import all

from Standard.Table import Table, Value_Type

from Standard.Test import Bench

polyglot java import java.lang.Long as Java_Long

options = Bench.options . set_warmup (Bench.phase_conf 3 5) . set_measure (Bench.phase_conf 3 5)

create_vectors num_rows =
    x = Vector.new num_rows i->
        i+1
    y = Vector.new num_rows i->
        if i % 10 < 2 then Java_Long.MAX_VALUE else i+1
    u = Vector.new num_rows i->
        10 + (i % 100)

    [x, y, u]

create_table : Table
create_table num_rows =
    v = create_vectors num_rows
    x = v.at 0
    y = v.at 1
    u = v.at 2

    t = Table.new [["X", x], ["Y", y], ["U", u]]

    assert condition =
        if condition.not then Panic.throw "Assertion failed"

    assert ((t.at "X" . value_type) == Value_Type.Integer)
    assert ((t.at "Y" . value_type) == Value_Type.Integer)
    assert ((t.at "U" . value_type) == Value_Type.Integer)
    t

foreign arrow int64_new = """
    new[Int64]

foreign arrow int64_plus = """
    +[Int64]

type Data
    private Value ~table ~arrow

    arrow_plus self = self.arrow.at 0
    arrow_x self = self.arrow.at 1
    arrow_y self = self.arrow.at 2
    arrow_u self = self.arrow.at 3

    create num_rows = Data.Value (create_table num_rows) <|
        column_to_arrow v:Vector -> Array =
            builder = int64_new.new v.length
            v.map e-> builder.append e
            builder.build

        v = create_vectors num_rows

        x = column_to_arrow (v.at 0)
        y = column_to_arrow (v.at 1)
        u = column_to_arrow (v.at 2)
        [int64_plus, x, y, u]


collect_benches = Bench.build builder->
    num_rows = 1000000
    data = Data.create num_rows

    builder.group ("Column_Arithmetic_" + num_rows.to_text) options group_builder->
        group_builder.specify "Plus_Fitting" <|
            (data.table.at "X") + (data.table.at "U")
        group_builder.specify "Plus_Overflowing" <|
            (data.table.at "Y") + (data.table.at "U")
        group_builder.specify "Multiply_Fitting" <|
            (data.table.at "X") * (data.table.at "U")
        group_builder.specify "Multiply_Overflowing" <|
            (data.table.at "Y") * (data.table.at "U")

    builder.group ("Arrow_Arithmetic_" + num_rows.to_text) options group_builder->
        bench_arrow_plus v1 v2 =
            data.arrow_plus v1 v2

        group_builder.specify "Plus_Fitting" <|
            bench_arrow_plus data.arrow_x data.arrow_u

main = collect_benches . run_main
