from Standard.Base import all

from Standard.Test import Bench, Faker, Phase_Conf


compare_all_adjacent text_vector =
    res = 1.up_to text_vector.length . fold False acc-> ix->
        compare_result = (text_vector.at ix-1) < (text_vector.at ix)
        acc && compare_result
    res


options = Bench.options . set_warmup (Phase_Conf.Seconds 2) . set_measure (Phase_Conf.Seconds 2)


collect_benches = Bench.build builder->
    ## The `Text` compare benchmarks check both scenarios where the Texts are
       short and very long - both checking the case where the difference appears
       early or late in the long string. This is to see well any overheads
       related to preprocessing the whole string and any possible early
       termination.

       The benchmarks are run in two variants - one where the generated texts
       contain only ASCII characters and another where the texts also contain
       non-ASCII letters and accent combinators modifying neighboring letters.
       This is to compare the performance of handling simple ASCII versus
       performance of handling more complex Unicode characters.
    bench_strcmp suite_prefix character_template common_prefix =
        faker = Faker.new
        ## Warning: this relies on the fact that Faker will treat the accent
           codepoint `\u{301}` as a separate code unit. We rely on this to add
           accents randomly to neighboring characters. If the implementation of
           Faker is changed, this must be modified accordingly.
        make_alpha_template length = Vector.new length _-> character_template
        very_short_template = make_alpha_template 4
        very_short = Vector.new 100000 _-> 'ðŸ¤©' + faker.string_value very_short_template
        medium_template = make_alpha_template 64
        medium = Vector.new 10000 _-> faker.string_value medium_template

        big_a_codepoint = 65
        big_template = make_alpha_template 100000
        big_random = Vector.new 100 _-> faker.string_value big_template
        big_early_difference = Vector.new 100 ix->
            "bb" + (Text.from_codepoints [big_a_codepoint + ix%5]) + "aaa" + (faker.string_value big_template)
        prefix = common_prefix.repeat 100000
        big_late_difference = Vector.new 100 ix-> prefix + (Text.from_codepoints [big_a_codepoint + ix%5])

        builder.group ("Text_Compare_" + suite_prefix) options group_builder->
            group_builder.specify "very_short" <|
                compare_all_adjacent very_short

            group_builder.specify "medium" <|
                compare_all_adjacent medium

            group_builder.specify "big_random" <|
                compare_all_adjacent big_random

            group_builder.specify "big_early_difference" <|
                compare_all_adjacent big_early_difference

            group_builder.specify "big_late_difference" <|
                compare_all_adjacent big_late_difference


    bench_strcmp "strcmp_Unicode" (Faker.upper_case_letters + 'Ä™\u{301}\u{302}'.char_vector) 'Ä™\u{301}'
    bench_strcmp "strcmp_ASCII" Faker.upper_case_letters 'A'


main = collect_benches . run_main
