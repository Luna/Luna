from Standard.Base import all

from Standard.Test import Bench

polyglot java import java.util.Random as Java_Random

gen_list len = 0.up_to len . fold List.Nil (l -> i -> List.Cons i+1 l)

sum_list_meta list =
    nil_cons = Meta.meta List.Nil . constructor
    folder acc list =
        meta_list = Meta.meta list
        if meta_list.constructor == nil_cons then acc else
            fs = meta_list.fields
            @Tail_Call folder (acc + fs.at 0) (fs.at 1)
    res = folder 0 list
    res

sum_recur n = if n == 0 then 0 else 1 + sum_recur n-1

build_map size =
    rand = Java_Random.new
    0.up_to size . fold Map.empty (m -> i -> m.insert (rand.nextInt 10000) i)

options = Bench.options

collect_benches = Bench.build builder->
    mil = 1000000
    list = gen_list mil
    vec = Vector.new mil (ix -> ix + 1)
    vec_decimal = Vector.new mil (ix -> ix + 0.0)

    builder.group "Collections" options group_builder->
        group_builder.specify "list_meta_fold" <|
            sum_list_meta list

        group_builder.specify "list_fold" <|
            list.fold 0 (+)

        group_builder.specify "vector_fold" <|
            vec.fold 0 (+)

        group_builder.specify "vector_decimal_fold" <|
            vec_decimal.fold 0 (+)

        group_builder.specify "build_map" <|
            build_map 10000


main = collect_benches . run_main
