from Base import all
import Base.System.Process
import Base.System.Process.Exit_Code
import Test

import Image.Codecs

polyglot java import java.lang.System as Java_System

spec =
    is_ci = Java_System.getenv "CI" == "true"
    if is_ci then here.spec_impl else Test.group "Codecs (skipped)"

fetch addr file =
    case Process.run_command "curl" [addr, "--silent", "--output", file.path] of
        Exit_Code.Exit_Success ->
            file
        Exit_Code.Exit_Failure code ->
            Test.fail ("Failed to fetch test image. Exit code: " + code.to_text)

spec_impl =
    rgba_addr = "https://upload.wikimedia.org/wikipedia/commons/thumb/e/e9/Hue_alpha_falloff.png/320px-Hue_alpha_falloff.png"
    rgba_file = here.fetch rgba_addr (Enso_Project.root / "rgba.png")
    Test.group "Codecs" <|
        Test.specify "should read color image" <|
            img = Codecs.read rgba_file
            img.rows.should_equal 160
            img.columns.should_equal 320
            img.channels.should_equal 3
        Test.specify "should read image as grayscale" <|
            img = Codecs.read rgba_file Codecs.Read_Grayscale
            img.rows.should_equal 160
            img.columns.should_equal 320
            img.channels.should_equal 1
        Test.specify "should read alpha channel" <|
            img = Codecs.read rgba_file Codecs.Read_Alpha_Channel
            img.rows.should_equal 160
            img.columns.should_equal 320
            img.channels.should_equal 4
