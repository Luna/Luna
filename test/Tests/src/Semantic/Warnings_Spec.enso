from Standard.Base import all

polyglot java import java.lang.Long

import Standard.Test

type My_Warning reason

type My_Type a b c
My_Type.my_method = this.a + this.b + this.c

type Wrap foo

rewrap w = case w of
    Wrap a -> Wrap a+1

poly_sum x y =
    Long.sum x y

get_foo x = x.foo

reassign_test x =
    consed = Wrap x
    reconsed = here.rewrap consed
    x1 = here.get_foo reconsed
    prim_sum = 1 + x1
    r = here.poly_sum prim_sum 1
    r

baz value = Warning.attach value "I have warned you"
bar value = here.baz value
foo value = here.bar value

spec = Test.group "Dataflow Warnings" <|
    Test.specify "should allow to attach multiple warnings and read them back" <|
        x = 1233
        y = Warning.attach x "don't do this"
        z = Warning.attach y "I'm serious"
        Warning.get_all z . map .value . should_equal ["I'm serious", "don't do this"]

    Test.specify "should thread warnings through constructor calls" <|
        z = Warning.attach 3 (My_Warning "warn!!!")
        y = Warning.attach 2 (My_Warning "warn!!")
        x = Warning.attach 1 (My_Warning "warn!")
        mtp = My_Type x y z
        mtp.should_equal (My_Type 1 2 3)
        Warning.get_all mtp . map .value . should_equal [My_Warning "warn!", My_Warning "warn!!", My_Warning "warn!!!"]

    Test.specify "should thread warnings through method calls"
        mtp = My_Type 1 2 3
        warned = Warning.attach mtp "omgggg"
        r = warned.my_method
        r.should_equal 6
        Warning.get_all r . map .value . should_equal ["omgggg"]

    Test.specify "should thread warnings through polyglot calls" <|
        y = Warning.attach 2 "warn!!"
        x = Warning.attach 1 "warn!"
        r = Long.sum x y
        r.should_equal 3
        Warning.get_all r . map .value . should_equal ['warn!', 'warn!!']

    Test.specify "should thread warnings through case expressions" <|
        z = Warning.attach 3 (My_Warning "warn!!!")
        y = Warning.attach 2 (My_Warning "warn!!")
        x = Warning.attach 1 (My_Warning "warn!")
        mtp = My_Type x y z
        r = case mtp of
            My_Type a b c -> a + b + c
        r.should_equal 6
        Warning.get_all r . map .value . should_equal [My_Warning "warn!", My_Warning "warn!!", My_Warning "warn!!!"]

    Test.specify "should attach correct stacktraces" <|
        current = Runtime.get_stack_trace
        warned = here.foo "value"
        warning_stack = Warning.get_all warned . head . origin
        relevant = warning_stack . drop_end current.length
        relevant.map .name . should_equal (['baz', 'bar', 'foo'].map ('Warnings_Spec.'+))

    Test.specify "should attach reassignment info in the last-reassigned-first order" <|
        x = Warning.attach 1 "warn!"
        r = here.reassign_test x
        warn = Warning.get_all r . head
        reassignments = warn.reassignments.map .name
        reassignments.should_equal ['Warnings_Spec.poly_sum', 'Small_Integer.+', 'Warnings_Spec.get_foo', 'Warnings_Spec.rewrap', 'Warnings_Spec.Wrap']


