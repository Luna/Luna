from Standard.Base import all

polyglot java import org.enso.base.Text_Utils
import Standard.Test

polyglot java import com.ibm.icu.text.BreakIterator
spec =
    Test.group "Text_Utils" <|
        kshi = '\u0915\u094D\u0937\u093F'
        facepalm = '\u{1F926}\u{1F3FC}\u200D\u2642\uFE0F'
        text = "a"+kshi+facepalm+'e\u{301}Z'
        codepoints_to_graphemes = _.flatten <| text.characters.map_with_index ix-> grapheme->
            codepoints_count = grapheme.utf_16.length
            Vector.new codepoints_count _->ix

        Test.specify "should correctly translate an codepoint index to a grapheme index" <|
            codepoints_to_graphemes . each_with_index codepoint_ix-> grapheme_ix->
                found_grapheme_ix = Text_Utils.codepoint_index_to_grapheme_index text codepoint_ix
                found_grapheme_ix.should_equal grapheme_ix

            Text_Utils.codepoint_index_to_grapheme_index text text.utf_16.length . should_equal text.length
            Text_Utils.codepoint_index_to_grapheme_index "" 0 . should_equal 0

            Text_Utils.codepoint_index_to_grapheme_index 'ą' 0 . should_equal 0
            Text_Utils.codepoint_index_to_grapheme_index 'ą' 1 . should_equal 1

            Text_Utils.codepoint_index_to_grapheme_index "aB" 0 . should_equal 0
            Text_Utils.codepoint_index_to_grapheme_index "aB" 1 . should_equal 1
            Text_Utils.codepoint_index_to_grapheme_index "aB" 2 . should_equal 2

            Text_Utils.codepoint_index_to_grapheme_index 'a\u{301}' 0 . should_equal 0
            Text_Utils.codepoint_index_to_grapheme_index 'a\u{301}' 1 . should_equal 0
            Text_Utils.codepoint_index_to_grapheme_index 'a\u{301}' 2 . should_equal 1

        Test.specify "should correctly translate a series of codepoint indices to a grapheme indices in a batch" <|
            translate_indices text ixes =
                Vector.from_array <| Text_Utils.codepoint_indices_to_grapheme_indices text ixes.to_array
            codepoint_indices = Vector.new text.utf_16.length ix->ix
            translate_indices text codepoint_indices . should_equal codepoints_to_graphemes

            translate_indices "" [0] . should_equal [0]
            translate_indices 'ą' [0, 1] . should_equal [0, 1]
            translate_indices "aB" [0, 1, 2] . should_equal [0, 1, 2]
            translate_indices 'a\u{301}' [0, 1, 2] . should_equal [0, 0, 1]

main = Test.Suite.run_main here.spec
