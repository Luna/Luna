from Standard.Base import all
from Standard.Base.Data.Array_Proxy import Array_Proxy

from Standard.Test import Test, Test_Suite

polyglot java import java.lang.IlllegalArgumentException

type Proxy_Object
    type Value length

    at : Integer -> Integer
    at ix = ix * 10

spec =
    Test.group "Array_Proxy" <|
        Test.specify "should correctly delegate to the callback" <|
            arr = Array_Proxy.new 3 (ix -> ix + 10)
            arr.length . should_equal 3
            arr.at 0 . should_equal 10
            arr.at 1 . should_equal 11
            arr.at 2 . should_equal 12
            arr.at 3 . should_fail_with Index_Out_Of_Bounds_Error_Data

        Test.specify "should be able to be used to construct a Vector" <|
            v = Vector.from_polyglot_array (Array_Proxy.new 3 (ix -> ix + 10))
            v.length . should_equal 3
            v . should_equal [10, 11, 12]

            v.map (x -> x + 1) . should_equal [11, 12, 13]

            v2 = Vector.from_polyglot_array (Array_Proxy.new 3 (ix -> v.at 2 - ix))
            v2.should_equal [12, 11, 10]
            v2.sort . should_equal [10, 11, 12]

        Test.specify "should be able to construct a Vector from a proxy object" <|
            v = Vector.from_polyglot_array (Array_Proxy.from_proxy_object [4, 3, 2])
            v.should_equal [4, 3, 2]

            v = Vector.from_polyglot_array (Array_Proxy.from_proxy_object (Proxy_Object.Value 5))
            v.should_equal [0, 10, 20, 30, 40]

        Test.specify "should check the callback type validity at construction" <|
            Text.expect_panic_with (Array_Proxy.new 0 0) IllegalArgumentException

main = Test_Suite.run_main spec
