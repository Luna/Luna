from Standard.Base import all

import Standard.Base.Network.HTTP.Request_Body.Request_Body
import Standard.Base.Errors.Illegal_Argument.Illegal_Argument

from Standard.Test import Test, Test_Suite
import Standard.Test.Extensions

polyglot java import java.lang.System as Java_System

type TestType
    Aaa (s:Text)
    Bbb (i:Integer)

type BadToJson
    Aaa (s:Text)
    to_json self = JS_Object.from_pairs [Pair.new [] 0]

spec =
    base_url = Java_System.getenv "ENSO_HTTP_TEST_HTTPBIN_URL"
    pending_has_url = if base_url != Nothing then Nothing else
        "The POST tests only run when the `ENSO_HTTP_TEST_HTTPBIN_URL` environment variable is set to URL of the httpbin server"

    Test.group "json" pending=pending_has_url <|
        base_url_with_slash = if base_url.ends_with "/" then base_url else base_url + "/"
        url_post = base_url_with_slash + "post"

        Test.specify "Can perform a POST" <|
            response = Data.post url_post "hello world"
            response.at "data" . should_equal "hello world"
            response.at "headers" . at "Content-Length" . should_equal "11"
            response.at "headers" . at "Content-Type" . should_equal "text/plain"

        Test.specify "Uses the correct mime type" <|
            body = '{ "a" : 1 }'
            Data.post url_post body . at "headers" . at "Content-Type" . should_equal "text/plain"
            Data.post url_post body mime_type="text/plain" . at "headers" . at "Content-Type" . should_equal "text/plain"
            Data.post url_post body mime_type="application/json" . at "headers" . at "Content-Type" . should_equal "application/json"
            Data.post url_post (TestType.Aaa "abcd") . at "headers" . at "Content-Type" . should_equal "application/json"
            Data.post url_post (TestType.Bbb 123) . at "headers" . at "Content-Type" . should_equal "application/json"
            Data.post url_post (Request_Body.Text "abcd") . at "headers" . at "Content-Type" . should_equal "text/plain"
            Data.post url_post (Request_Body.Json '{ "a" : 1 }') . at "headers" . at "Content-Type" . should_equal "application/json"

        Test.specify "Correctly posts a file" <|
            test_file = enso_project.data / "sample.txt"
            returned_file = Data.post url_post test_file . at "data"
            returned_file.should_equal test_file.read_text

        Test.specify "Correctly posts a file via a form" <|
            test_file = enso_project.data / "sample.txt"
            Data.post_file_as_form url_post test_file "fn" . at "data" . should_contain "fn=Cupcake+ipsum+dolor+sit+amet"

        Test.specify "can handle a bad .to_json" <|
            Data.post url_post (BadToJson.Aaa "abcd") . should_fail_with Illegal_Argument

main = Test_Suite.run_main spec
