from Standard.Base import all

import Standard.Base.Errors.Illegal_Argument.Illegal_Argument
import Standard.Base.Runtime.Context

from Standard.Test import all


type My_Type

type My_Error
    Error

bar = Runtime.get_stack_trace
baz = bar
Number.foo self = baz
foo x = x.foo
My_Type.foo self = foo 123

deep_e = Error.throw (Illegal_Argument.Error "ie")
deep_d = deep_e
deep_c = deep_d
deep_b = deep_c
deep_a = deep_b

add_specs suite_builder = suite_builder.group "Stack traces" group_builder->
    group_builder.specify "avoid handler frames in stack" <|
        lines = Vector.build b->
            do_the_handling caught_panic =
                st = caught_panic.stack_trace
                st.each el->
                    b.append el.to_display_text

            my_exception_handler caught_panic =
                do_the_handling caught_panic

            f5 ~a = a
            f4 ~a = f5 a...
            f3 ~a =
                Panic.catch Any handler=my_exception_handler <|
                    f4 a...
            f2 ~a = f3 a...
            f1 ~a = f2 a...

            f1 (Panic.throw My_Error.Error)

        top_lines = lines.take (..While l-> l.contains "Vector.type" . not)
        out = top_lines.join '\n'

        forbidden1 = out.contains "my_exception_handler"
        forbidden2 = out.contains "do_the_handling"

        if forbidden1 || forbidden2 then
            Test.fail 'Frames from exception handling should not be in the stacktrace:\n'+out

    group_builder.specify "should capture traces correctly" <|
        modname = Meta.get_simple_type_name Stack_Traces_Spec
        stack = My_Type.foo
        names = [modname + ".bar", modname + ".baz", "Number.foo", modname + ".foo", "My_Type.foo"]
        stack.take (..First 5) . map .name . should_equal names
        file = enso_project.root / 'src' / 'Runtime' / 'Stack_Traces_Spec.enso'
        stack.take (..First 5) . map (.source_location >> .file) . each (_.should_equal file)

    group_builder.specify "should respect Runtime.Context.Dataflow_Stack_Trace (for error thrown from Enso)" <|
        modname = Meta.get_simple_type_name Stack_Traces_Spec
        names = [modname + ".deep_e", modname + ".deep_d", modname + ".deep_c", modname + ".deep_b", modname + ".deep_a"]

        shallow_stack_trace = deep_a.stack_trace
        shallow_stack_trace.length . should_equal 1
        shallow_stack_trace.at 0 . name . should_equal (names.at 0)

        Context.Dataflow_Stack_Trace.with_enabled <|
            deep_stack_trace = deep_a.stack_trace
            (deep_stack_trace.length > 5) . should_be_true
            deep_stack_trace.take 5 . map .name . should_equal names

main =
    suite = Test.build suite_builder->
        add_specs suite_builder
    suite.run_with_filter
