from Standard.Base import all
import Standard.Base.Errors.Common.Index_Out_Of_Bounds
import Standard.Base.Errors.Common.Unsupported_Argument_Types
import Standard.Base.Data.Polyglot_Array_Builder.Polyglot_Array_Builder

from Standard.Test import all

foreign arrow new_arrow = """
    new[Int8]

Polyglot_Array_Builder.set self index value = Polyglot_Array_Builder.set_builtin self index value

add_specs suite_builder =
    pending = if Polyglot.is_language_installed "arrow" then Nothing else """
        Can't run Arrow tests, Arrow is not installed.

    suite_builder.group "Arrow" pending=pending group_builder->
        group_builder.specify "should allow for creating a new fixed Array in Arrow" <|
            arrow_array_constructor = new_arrow
            array_10 = arrow_array_constructor.new 10
            builder = Polyglot_Array_Builder.from_polyglot_array array_10

            builder.length . should_equal 10
            builder.at 1 . should_equal Nothing
            builder.at 10 . should_fail_with Index_Out_Of_Bounds
            builder.append 2
            builder.append 10
            builder.at 1 . should_equal 10
            builder.set 8 127
            builder.set 8 128 . should_fail_with Unsupported_Argument_Types
            v = builder.build
            v.at 8 . should_equal 127
            builder.set 1 0 . should_fail_with Unsupported_Argument_Types
            Polyglot.write_array_element array_10 1 0
            builder.at 1 . should_equal 0
            v.at 1 . should_equal 0 # dangerous but allowed

main =
    suite = Test.build suite_builder->
        add_specs suite_builder
    suite.run_with_filter
